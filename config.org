#+BEGIN_SRC emacs-lisp
   ;;; -*- lexical-binding: t -*-
#+END_SRC

* Variables
#+BEGIN_SRC emacs-lisp
  (defvar eqyiel/home-directory
    (or (and (getenv "HOME")
             (directory-file-name
              (expand-file-name (getenv "HOME"))))
        (directory-file-name (expand-file-name "~/"))))

  (defvar eqyiel/xdg-cache-home
    (or (and (getenv "XDG_CACHE_HOME")
             (directory-file-name
              (expand-file-name (getenv "XDG_CACHE_HOME"))))
        (concat
         (directory-file-name
          (expand-file-name eqyiel/home-directory))
         "/.cache")))

  (defvar eqyiel/xdg-config-home
    (or (and (getenv "XDG_CONFIG_HOME")
             (directory-file-name
              (expand-file-name (getenv "XDG_CONFIG_HOME"))))
        (concat
         (directory-file-name
          (expand-file-name eqyiel/home-directory))
         "/.config")))

  (defvar eqyiel/xdg-data-home
    (or (and (getenv "XDG_DATA_HOME")
             (expand-file-name (getenv "XDG_DATA_HOME")))
        (concat
         (directory-file-name
          (expand-file-name eqyiel/home-directory))
         "/.local/share")))

  (defvar eqyiel/mail-directory
    (concat
     (directory-file-name
      (expand-file-name eqyiel/home-directory))
     "/mail"))

  (defvar eqyiel/news-directory
    (concat
     (directory-file-name
      (expand-file-name eqyiel/home-directory))
     "/mail"))

  (defvar eqyiel/downloads-directory
    (concat
     (directory-file-name
      (expand-file-name eqyiel/home-directory))
     "/downloads"))

  (defvar eqyiel/env
    (if (and
         (eq system-type 'darwin)
         (string-equal
          (shell-command-to-string "printf %s \"$(hostname)\"")
          "PC2218"))
        'work
      'home))

  (defvar eqyiel/org-directory
    (concat
     (directory-file-name
      (expand-file-name eqyiel/home-directory))
     (if (eq eqyiel/env 'work)
         "/sync/org"
       "/org")))

  (defvar eqyiel/timezone "Asia/Tokyo")
#+END_SRC

* Make cache and data directories if they don't exist
#+BEGIN_SRC emacs-lisp
  (when (not (car (file-attributes (concat eqyiel/xdg-cache-home "/emacs"))))
    (make-directory (concat eqyiel/xdg-cache-home "/emacs")))

  (when (not (car (file-attributes (concat eqyiel/xdg-data-home "/emacs"))))
    (make-directory (concat eqyiel/xdg-data-home "/emacs")))
#+END_SRC

* General defaults
#+BEGIN_SRC emacs-lisp
  (setq user-full-name "Ruben Maher"
        user-mail-address "ruben@maher.fyi"
        mail-host-address "maher.fyi")

  (menu-bar-mode -1)
  (scroll-bar-mode -1)
  (tool-bar-mode -1)
  (transient-mark-mode 1)
  (line-number-mode 1)
  (column-number-mode 1)
  (show-paren-mode 1)
  (blink-cursor-mode 1)
  (global-hl-line-mode 1)
  (delete-selection-mode 1)
  (xterm-mouse-mode 1)

  (setq-default
   indent-tabs-mode nil
   fill-column 80
   tab-width 2
   default-tab-width 2
   standard-indent 2
   require-final-newline t
   sentence-end-double-space t
   eval-expression-print-length nil)

  (set-language-environment "UTF-8")

  (defalias 'yes-or-no-p 'y-or-n-p)
  (defvaralias 'c-basic-offset 'tab-width)
  (defvaralias 'cperl-indent-level 'tab-width)

  ;; Give me two spaces everywhere
  (add-hook 'c-mode-common-hook #'(lambda () (setq c-basic-offset tab-width)))

  (set-locale-environment "en_US.UTF-8")

  (setq buffer-file-coding-system 'utf-8-unix
        default-file-name-coding-system 'utf-8-unix
        default-keyboard-coding-system 'utf-8-unix
        default-process-coding-system '(utf-8-unix . utf-8-unix)
        default-sendmail-coding-system 'utf-8-unix
        default-terminal-coding-system 'utf-8-unix)

  (eval-after-load "warnings"
    '(setq display-warning-minimum-level :error))

  (if (eq system-type 'darwin)
      (setq browse-url-browser-function 'browse-url-default-macosx-browser)
    (setq browse-url-browser-function 'browse-url-generic
          browse-url-generic-program "firefox"))

  ;; Stop nagging me to enable these useful commands.
  (dolist (x '(dired-find-alternate-file
               upcase-region
               downcase-region
               narrow-to-region))
    (put x 'disabled nil))

  ;; Don't litter my ~/.emacs.d.
  (setq
   backup-by-copying t
   backup-directory-alist
   `(("." . ,(concat eqyiel/xdg-cache-home "/emacs/backup")))
   delete-old-versions t
   kept-new-versions 2
   kept-old-versions 2
   version-control t
   vc-make-backup-files t
   echo-keystrokes 0.1
   auto-save-default nil
   auto-save-list-file-prefix
   (concat eqyiel/xdg-cache-home "/emacs/backup/.saves-")
   bookmark-default-file
   (concat eqyiel/xdg-cache-home "/emacs/emacs.bmk")
   url-cache-directory
   (concat eqyiel/xdg-cache-home "/emacs/url-cache")
   eshell-directory-name
   (concat eqyiel/xdg-cache-home "/emacs/eshell")
   custom-file
   (concat
    (directory-file-name
     (expand-file-name user-emacs-directory))
    "/eqyiel-custom-junk.el"))

  ;; No really.
  (eval-after-load "kkc"
    `(setq kkc-init-file-name
           ,(concat eqyiel/xdg-cache-home "/emacs/kkcrc")))

  (setq apropos-do-all t
        inhibit-startup-message t
        ediff-window-setup-function 'ediff-setup-windows-plain
        ediff-custom-diff-options "-u"
        read-buffer-completion-ignore-case t
        read-file-name-completion-ignore-case t
        scroll-error-top-bottom t
        scroll-preserve-screen-position 1
        scroll-step 1
        save-interprogram-paste-before-kill t
        frame-title-format '(buffer-file-name "%f" ("%b"))
        x-select-enable-clipboard t
        x-select-enable-primary t
        x-stretch-cursor t)

  ;; Automagically refresh buffers if they are changed on disk.
  (global-auto-revert-mode)
  ;; Same for directories, but be quiet about it please.
  (setq global-auto-revert-non-file-buffers t
        auto-revert-verbose nil)

  (setq delete-by-moving-to-trash t)

  ;; http://stackoverflow.com/a/6830894/2204400
  (add-hook
   'before-save-hook
   (lambda ()
     (when buffer-file-name
       (let ((dir (file-name-directory buffer-file-name)))
         (when (and (not (file-exists-p dir))
                    (y-or-n-p
                     (format "Directory %s does not exist. Create it?"
                             dir)))
           (make-directory dir t))))))

  (global-set-key (kbd "<C-mouse-5>") 'text-scale-increase)
  (global-set-key (kbd "<C-mouse-4>") 'text-scale-decrease)
  (global-set-key (kbd "<C-up>") 'text-scale-increase)
  (global-set-key (kbd "<C-down>") 'text-scale-decrease)
  (global-set-key [f11] 'toggle-frame-fullscreen)
  ;; Type a keybinding and have it appear
  (global-set-key (kbd "C-c e") 'edmacro-insert-key)
  ;; Don't accidentally tap `suspend-frame'
  (global-unset-key (kbd "C-x C-z"))
  (global-unset-key (kbd "C-z"))

  ;; Makefiles are very particular about tabs.
  (add-hook 'makefile-mode-hook
            (lambda () (setq indent-tabs-mode t tab-width 8)))

  ;; Open systemd service files with an appropriate mode.
  (add-to-list 'auto-mode-alist '("\\.service\\'" . conf-unix-mode))
  (add-to-list 'auto-mode-alist '("\\.target\\'" . conf-unix-mode))
  (add-to-list 'auto-mode-alist '("\\.timer\\'" . conf-unix-mode))

  ;; Use `eldoc' in `emacs-lisp-mode' buffers.
  (add-hook 'emacs-lisp-mode-hook 'turn-on-eldoc-mode)

  ;; Make everything not wrong on macOS
  (setq mac-option-modifier 'meta
        mac-command-modifier 'super)

#+END_SRC

* Fonts
#+BEGIN_SRC emacs-lisp
  (cond
   ((eq system-type 'darwin) ;; high dpi
    (set-face-attribute 'default nil :height 140 :family "DejaVu Sans Mono"))
   ((eq system-type 'windows-nt) ;; dpi :(
    (set-face-attribute 'default nil :height 100 :family "DejaVu Sans Mono"))
   (t
    (set-face-attribute 'default nil :height 120 :family "DejaVu Sans Mono")))

  (defun eqyiel/set-emoji-font (frame)
    "Adjust the font settings of FRAME so Emacs can display emoji properly."
    (if (eq system-type 'darwin)
        ;; NS/Cocoa
        ;; This doesn't actually work anymore.
        ;; See:
        ;; lunaryorn.com/posts/bye-bye-emojis-emacs-hates-macos.html
        (set-fontset-font t 'symbol (font-spec :family "Apple Color Emoji")
                          frame 'prepend)
        ;; (set-fontset-font t 'symbol (font-spec :family "Noto Emoji")
        ;;                 frame 'prepend)
      ;; GNU/Linux
      (set-fontset-font t 'symbol (font-spec :family "Noto Emoji")
                        frame 'prepend)))

  ;; For when Emacs is started in GUI mode:
  (eqyiel/set-emoji-font nil)
  ;; Hook for when a frame is created with emacsclient
  ;; see https://www.gnu.org/software/emacs/manual/html_node/elisp/Creating-Frames.html
  (add-hook 'after-make-frame-functions 'eqyiel/set-emoji-font)
#+END_SRC

* Bootstrap ~use-package~
#+BEGIN_SRC emacs-lisp
    (require 'package)
    (setq package-enable-at-startup nil
          package-user-dir "~/.emacs.d/site-lisp/elpa"
          package-gnupghome-dir "~/.emacs.d/site-lisp/elpa/gnupg"
          package-archives nil)

    ;; Bootstrap `use-package' and its dependencies if they are not already
    ;; available.
    (let ((dependencies '(use-package diminish bind-key)))
      (unless (seq-reduce (lambda (prev next) (and prev next))
                          (mapcar 'package-installed-p dependencies) t)
        (package-refresh-contents)
        (dolist (package dependencies)
          (unless (package-installed-p package)
            (package-install package)))))

    (eval-when-compile
      (require 'use-package))
    (require 'diminish)
    (require 'bind-key)

    (setq use-package-always-ensure nil
          use-package-always-defer t
          ;; When using :hook omit the "-hook" suffix if you specify the hook
          ;; explicitly, as this is appended by default. For example the following
          ;; code will not work as it attempts to add to the prog-mode-hook-hook
          ;; which does not exist:
          ;; ;; DOES NOT WORK
          ;; (use-package ace-jump-mode
          ;;   :hook (prog-mode-hook . ace-jump-mode))
          ;; If you do not like this behaviour, set use-package-hook-name-suffix
          ;; to nil. By default the value of this variable is "-hook".
          ;;
          ;; https://github.com/jwiegley/use-package/tree/d2640fec376a8458a669e7526e63e5870d875118#hooks
          ;;
          ;; Disable because it makes -hook names fail the grep test.
          ;; http://jamie-wong.com/2013/07/12/grep-test/
          use-package-hook-name-suffix nil)

    (use-package use-package-chords :config (key-chord-mode t) :demand t)
#+END_SRC

* Utility functions
#+BEGIN_SRC emacs-lisp
  (use-package dash :demand t)

  (use-package s :demand t)

  (defun eqyiel/kill-region-or-backward-kill-word (&optional arg region)
    "`kill-region' if the region is active, otherwise `backward-kill-word'

        Taken from: http://david.rothlis.net/emacs/ergonomics.html"
    (interactive
     (list (prefix-numeric-value current-prefix-arg) (use-region-p)))
    (if region (kill-region (region-beginning) (region-end))
      (backward-kill-word arg)))

  (bind-key "C-w" 'eqyiel/kill-region-or-backward-kill-word)

  (defun eqyiel/local-comment-auto-fill ()
    "Taken from: https://github.com/technomancy/emacs-starter-kit"
    (set (make-local-variable 'comment-auto-fill-only-comments) t)
    (auto-fill-mode t))

  (add-hook 'prog-mode-hook 'eqyiel/local-comment-auto-fill)

  (defun eqyiel/sudo-edit (&optional arg)
    "Edit currently visited file as root.  With a prefix ARG prompt for a file to
        visit.  Will also prompt for a file to visit if current buffer is not visiting a
        file.

        Taken from: http://emacsredux.com/blog/2013/04/21/edit-files-as-root/"
    (interactive "P")
    (if (or arg (not buffer-file-name))
        (find-file (concat "/sudo:root@localhost:"
                           (ido-read-file-name "Find file(as root): ")))
      (find-alternate-file (concat "/sudo:root@localhost:" buffer-file-name))))

  (defun eqyiel/eval-and-replace ()
    "Replace the preceding sexp with its value.

        Taken from: http://emacsredux.com/blog/2013/06/21/eval-and-replace/"
    (interactive)
    (backward-kill-sexp)
    (condition-case nil
        (prin1 (eval (read (current-kill 0)))
               (current-buffer))
      (error (message "Invalid expression")
             (insert (current-kill 0)))))

  (defun eqyiel/insert-date ()
    "Insert today's date."
    (interactive)
    (let ((t0 (current-time)))
      (insert (completing-read "Select format: "
                               `(,(format-time-string "<%F %a %T>" t0)
                                 ,(format-time-string "<%F %a>" t0)
                                 ,(format-time-string "%s" t0)
                                 ,(format-time-string "%R" t0)
                                 ,(format-time-string "%T" t0))))))

  (bind-key "C-c d" 'eqyiel/insert-date)

  (defun eqyiel/open-line-below ()
    "Taken from: http://whattheemacsd.com/editing-defuns.el-01.html"
    (interactive)
    (end-of-line)
    (newline)
    (indent-for-tab-command))

  (bind-key "C-o" 'eqyiel/open-line-below)

  (defun eqyiel/open-line-above ()
    "Taken from: http://whattheemacsd.com/editing-defuns.el-01.html"
    (interactive)
    (beginning-of-line)
    (newline)
    (forward-line -1)
    (indent-for-tab-command))

  (bind-key "C-S-o" 'eqyiel/open-line-above)

  (defun eqyiel/comint-delchar-or-eof-or-kill-buffer (arg)
    "C-d on an empty line in the shell terminates the process.

        Taken from: http://whattheemacsd.com/setup-shell.el-01.html"
    (interactive "p")
    (if (null (get-buffer-process (current-buffer)))
        (kill-buffer)
      (comint-delchar-or-maybe-eof arg)))

  (defun eqyiel/rotate-windows ()
    "Rotate your windows.

        Taken from: http://whattheemacsd.com/buffer-defuns.el-02.html"
    (interactive)
    (cond ((not (> (count-windows)1))
           (message "You can't rotate a single window!"))
          (t
           (setq i 1)
           (setq num-windows (count-windows))
           (while  (< i num-windows)
             (let* (
                    (w1 (elt (window-list) i))
                    (w2 (elt (window-list) (+ (% i num-windows) 1)))

                    (b1 (window-buffer w1))
                    (b2 (window-buffer w2))

                    (s1 (window-start w1))
                    (s2 (window-start w2))
                    )
               (set-window-buffer w1  b2)
               (set-window-buffer w2 b1)
               (set-window-start w1 s2)
               (set-window-start w2 s1)
               (setq i (1+ i)))))))

  (bind-key "H-<return>" 'eqyiel/rotate-windows)
  (bind-key "M-s-S-C-<return>" 'eqyiel/rotate-windows)

  (defun eqyiel/toggle-window-split ()
    "Taken from: http://whattheemacsd.com/buffer-defuns.el-03.html"
    (interactive)
    (if (= (count-windows) 2)
        (let* ((this-win-buffer (window-buffer))
               (next-win-buffer (window-buffer (next-window)))
               (this-win-edges (window-edges (selected-window)))
               (next-win-edges (window-edges (next-window)))
               (this-win-2nd (not (and (<= (car this-win-edges)
                                           (car next-win-edges))
                                       (<= (cadr this-win-edges)
                                           (cadr next-win-edges)))))
               (splitter
                (if (= (car this-win-edges)
                       (car (window-edges (next-window))))
                    'split-window-horizontally
                  'split-window-vertically)))
          (delete-other-windows)
          (let ((first-win (selected-window)))
            (funcall splitter)
            (if this-win-2nd (other-window 1))
            (set-window-buffer (selected-window) this-win-buffer)
            (set-window-buffer (next-window) next-win-buffer)
            (select-window first-win)
            (if this-win-2nd (other-window 1))))))

  (bind-key "H-SPC" 'eqyiel/toggle-window-split)
  (bind-key "M-s-S-C-SPC" 'eqyiel/toggle-window-split)

  (defun eqyiel/open-width ()
    "Simple function that allows us to open the underlying
        file of a buffer in an external program.

        Taken from: https://github.com/bbatsov/prelude/blob/master/core/prelude-core.el"
    (interactive)
    (when buffer-file-name
      (shell-command
       (concat
        (if (eq system-type 'darwin)
            "open"
          (read-shell-command "Open current file with: "))
        " "
        buffer-file-name))))

  (defun eqyiel/duckduckgo ()
    "DDG a query or region if any."
    (interactive)
    (browse-url
     (concat "https://duckduckgo.com/?q="
             (url-hexify-string
              (if (use-region-p)
                  (buffer-substring (region-beginning) (region-end))
                (read-string "DuckDuckGo: "))))))

  (defun eqyiel/copy-file-name-to-clipboard ()
    "Copy the path to the current `buffer-file-name' to the clipboard.  If
  `universal-argument' is given, use absolute path.  Otherwise: try to copy
  relative path from repository root, or just copy buffer name if this is not a
  git directory."
    (interactive)
    (let ((path (let ((filename
                       (if (equal major-mode 'dired-mode)
                           default-directory
                         (buffer-file-name))))
                  (if filename
                      (if (equal current-prefix-arg nil) ; Was C-u provided?
                          (let ((repository-root-dir (vc-root-dir)))
                            (if repository-root-dir
                                (s-chop-prefix
                                 (expand-file-name repository-root-dir)
                                 filename)
                              filename))
                        filename)
                    (buffer-name)))))
      (progn
        (kill-new path)
        (message "Copied buffer file name '%s' to the clipboard." path))))

  (bind-key "C-c w" 'eqyiel/copy-file-name-to-clipboard)

  (defun eqyiel/rename-file-and-buffer ()
    "Renames current buffer and file it is visiting.

        http://whattheemacsd.com/file-defuns.el-01.html"
    (interactive)
    (let ((name (buffer-name))
          (filename (buffer-file-name)))
      (if (not (and filename (file-exists-p filename)))
          (error "Buffer '%s' is not visiting a file!" name)
        (let ((new-name (read-file-name "New name: " filename)))
          (if (get-buffer new-name)
              (error "A buffer named '%s' already exists!" new-name)
            (rename-file filename new-name 1)
            (rename-buffer new-name)
            (set-visited-file-name new-name)
            (set-buffer-modified-p nil)
            (message "File '%s' successfully renamed to '%s'"
                     name (file-name-nondirectory new-name)))))))

  (bind-key "C-x C-r" 'eqyiel/rename-file-and-buffer)

  (defun eqyiel/delete-file-and-buffer ()
    "Removes file connected to current buffer and kills buffer.

        http://whattheemacsd.com/file-defuns.el-02.html"
    (interactive)
    (let ((filename (buffer-file-name))
          (buffer (current-buffer))
          (name (buffer-name)))
      (if (not (and filename (file-exists-p filename)))
          (ido-kill-buffer)
        (when (yes-or-no-p "Are you sure you want to remove this file? ")
          (delete-file filename)
          (kill-buffer buffer)
          (message "File '%s' successfully removed" filename)))))

  (bind-key "C-x C-k" 'eqyiel/delete-file-and-buffer)

  (defun eqyiel/rotn-region (n)
    "Decode a caesar cipher.  Adapted from `rot13' to shift by N."
    (interactive "NHow many? ")
    (if (use-region-p)
        (let ((rotn-translate-table
               (let ((str (make-string 127 0))
                     (i 0))
                 (while (< i 127)
                   (aset str i i)
                   (setq i (1+ i)))
                 (setq i 0)
                 (while (< i 26)
                   (aset str (+ i ?a) (+ (% (+ i n) 26) ?a))
                   (aset str (+ i ?A) (+ (% (+ i n) 26) ?A))
                   (setq i (1+ i)))
                 str)))
          (translate-region (region-beginning) (region-end) rotn-translate-table))
      (message "Mark a region first.")))

  (defun eqyiel/print-to-pdf (dest)
    "Pretty-print a buffer using PostScript and save it as a PDF."
    (interactive "FSave to where? ")
    (let ((tmp (substring (shell-command-to-string "mktemp") 0 -1)))
      (ps-spool-buffer-with-faces)
      (switch-to-buffer "*PostScript*")
      (write-file tmp)
      (kill-buffer (file-name-nondirectory tmp))
      (shell-command (concat "ps2pdf14 " tmp " " dest))
      (shell-command (concat "rm " tmp))
      (message (concat "PDF written to " dest "."))))

  ;; http://www.emacswiki.org/emacs/EmacsAsDaemon#toc9
  (defun eqyiel/server-shutdown ()
    "Save buffers, quit, and shutdown (kill) server."
    (interactive)
    (save-some-buffers)
    (kill-emacs))

  (defun eqyiel-count-commas ()
    "CSV files are a pain to read, use this to see if there are as many commas as
        there should be."
    (interactive)
    (let ((i 0))
      (beginning-of-line)
      (while (re-search-forward "," (line-end-position) t)
        (setq i (+ i 1)))
      (message "found %s" i)))

  (defun eqyiel/copy-rectangle-to-kill-ring (start end)
    "Saves a rectangle to the normal kill ring."
    (interactive "r")
    (let ((lines (extract-rectangle start end)))
      (with-temp-buffer
        (while lines
          (insert-for-yank (car lines))
          (insert "\n")
          (setq lines (cdr lines)))
        (kill-ring-save (point-min) (point-max)))))

  (defun eqyiel/parent-directory (dir)
    (file-name-directory
     (directory-file-name
      dir)))

  ;; https://www.emacswiki.org/emacs/SortWords
  (defun eqyiel/sort-words (reverse beg end)
    "Sort words in region alphabetically, in REVERSE if negative.
        Prefixed with negative \\[universal-argument], sorts in reverse.

        The variable `sort-fold-case' determines whether alphabetic case affects the
        sort order.

        See `sort-regexp-fields'."
    (interactive "P\nr")
    (sort-regexp-fields reverse "\\w+" "\\&" beg end))

  (defun eqyiel/sort-lines-or-words (reverse beg end)
    "Sort lines if active region covers more than one line, otherwise sort words."
    (interactive "P\nr")
    (if (> (count-lines beg end) 1)
        (sort-lines reverse beg end)
      (eqyiel/sort-words reverse beg end)))

  (global-set-key [f9] 'eqyiel/sort-lines-or-words)
  (global-set-key (kbd "C-c 9") 'eqyiel/sort-lines-or-words)

  ;; Make life better in SSH sessions
  (defun eqyiel/copy-to-clipboard (beg end &optional region)
    (when (executable-find "copy-to-clipboard")
      (let ((inhibit-message t))
        (shell-command-on-region beg end "copy-to-clipboard"))))

  (advice-add 'kill-region :after 'eqyiel/copy-to-clipboard)
  (advice-add 'copy-region-as-kill :after 'eqyiel/copy-to-clipboard)

  (defun eqyiel/slugify (string &optional delimiter)
    (let ((actual-delimiter (or delimiter "-")))
      (replace-regexp-in-string
       (rx (any " _")) actual-delimiter
       (downcase (replace-regexp-in-string "[^A-Za-z0-9 ]" "" string)))))

  (defun eqyiel/get-last-messages-buffer-message ()
    "Get the last line logged to the messages buffer."
    (save-excursion
      (set-buffer "*Messages*")
      (save-excursion
        (forward-line (- 1))
        (backward-char)
        (let ((end (point)))
          (forward-line 0)
          (buffer-substring-no-properties (point) end)))))

  (defun eqyiel/eslint-get-rule-violation-at-point ()
    (interactive)
    "Get the last eslint rule violation that was logged to `*Messages*'.

  This is a hack and it depends on the violated rule being logged within square
  brackets like [react/require-default-props]"
    (let* ((target-message (eqyiel/get-last-messages-buffer-message))
           (eslint-rule (and (string-match "\\[\\(.*\\)\\]" target-message)
                             (match-string 1 target-message))))
      (if eslint-rule (progn
                        (kill-new eslint-rule)
                        (message "Saved to kill ring: %s" eslint-rule))
        (message "No eslint warnings found at point."))))

  (defun eqyiel/psql ()
    (interactive)
    (require 'sql)
    (if (member
         nil
         (mapcar
          (lambda (element)
            (getenv element))
          '("PGHOST" "PGPORT" "PGUSER" "PGPASSWORD" "PGDATABASE")))
        ;; if required environment not set, fall back to interactive login
        (sql-postgres)
      (switch-to-buffer (sql-comint-postgres 'postgres sql-postgres-options))))

  (defun eqyiel/create-markdown-diary-entry-for-today ()
    (interactive)
    (let ((target-file-name
           (concat
            (directory-file-name (expand-file-name eqyiel/home-directory))
            "/sync/diary/" (format-time-string "%F.md" (current-time)))))
      (unless (file-exists-p target-file-name)
        (save-excursion
          (find-file target-file-name)
          (insert (concat "#" (format-time-string " %A %F%n%n" (current-time))))))
      (find-file target-file-name)))

  (defun eqyiel/yank-rectangle-with-insert ()
  "Like `yank-rectangle', but inserts lines rather than inserting amongst following lines.

  https://emacs.stackexchange.com/questions/19461/insert-lines-when-yanking-rectangle-rather-than-inserting-among-following-lines"
    (interactive)
    (narrow-to-region (point) (mark))
    (yank-rectangle)
    (widen))

  (global-set-key (kbd "C-x r C-y") 'eqyiel/yank-rectangle-with-insert)
#+END_SRC

** Functions for working with node projects

#+BEGIN_SRC emacs-lisp
  (use-package dash :demand t)
  (use-package f :demand t)
  (use-package s :demand t)
  (use-package json :demand t)

  (defun eqyiel/node-json-read-file-safely (json-file)
    "Read JSON-FILE and return its contents or nil if it was malformed."
    (condition-case nil
        ;; silence errors from `json-read-file' if package.json is malformed
        ;; (for example, while resolving merge conflicts)
        (when (and (f-readable-p json-file)
                   (f-exists-p json-file))
          (json-read-file json-file))
      (error nil)))

  (defun eqyiel/node-find-js-project-roots (&optional directory js-project-roots)
    "Find JS-PROJECT-ROOTS (containing package.json) starting with DIRECTORY.

  Returns a list of candidate directories, including the current working directory
  if no package.json was found."
    (let* ((package-file "package.json")
           (actual-default-directory
            (or directory
                (buffer-file-name)
                default-directory))
           (dominating-file
            (or (locate-dominating-file actual-default-directory package-file)
                actual-default-directory))
           (parent-dominating-file
            (if dominating-file
                (locate-dominating-file
                 (or (f-dirname dominating-file) actual-default-directory)
                 package-file)
              actual-default-directory)))
      (if (or (not parent-dominating-file)
              (s-equals? dominating-file parent-dominating-file))
          (reverse (cons dominating-file js-project-roots))
        (eqyiel/node-find-js-project-roots
         parent-dominating-file
         (cons dominating-file js-project-roots)))))

  (defun eqyiel/node-find-dependency-in-package-file (package-file dependency)
    "Return t if PACKAGE-FILE has DEPENDENCY.

  PACKAGE-FILE should be is the absolute path to \"package.json\".  DEPENDENCY
  should be a list of symbols in which case the first one that is found will
  return or a string.

  It's useful to use a list of symbols for two reasons:

  - in the event that the package has a different \"bin\" key than its name in
    package.json, for example flow is called \"flow-bin\" on NPM but its bin key
    is \"flow\".
  - in the event that there is a package that has a drop in replacement available
    with a different name, like \"prettier\" and \"prettier-eslint-cli\"."
    (let* ((package (eqyiel/node-json-read-file-safely package-file))
           (node-dependency
            (caar
             (delq
              nil
              (mapcar
               (lambda (element)
                 (assoc (cdr element)
                        (assoc (car element) package)))
               (-mapcat
                (lambda (item)
                  (-concat `(,(cons 'dependencies item)
                             ,(cons 'devDependencies item))))

                (if (listp dependency)
                    dependency (list (intern dependency)))))))))
      (when node-dependency (symbol-name node-dependency))))

  (defun eqyiel/node-package-path-exists-p (js-project-root dependency)
    "Construct the path in JS-PROJECT-ROOT to DEPENDENCY."
    (let* ((dependency-package-path
            (eqyiel/node-find-dependency-package-path
             js-project-root dependency)))
      (and (f-readable-p dependency-package-path)
           (f-exists-p dependency-package-path))))

  (defun eqyiel/node-should-use-package-p (js-project-roots dependency)
    "Check JS-PROJECT-ROOTS for DEPENDENCY in both package.json and node_modules.

  DEPENDENCY should be a list of symbols (in which case the first one that is
  found will return) or a string.  If there are multiple JS-PROJECT-ROOTS with
  that dependency, the nearest one will be used."
    (car
     (delq
      nil
      (mapcar
       (lambda (js-project-root)
         (let ((resolved-dependency
                (eqyiel/node-find-dependency-in-package-file
                 (f-join js-project-root "package.json") dependency)))
           (and resolved-dependency
                (eqyiel/node-package-path-exists-p
                 js-project-root resolved-dependency))))
       js-project-roots))))

  (defun eqyiel/node-find-dependency-package-path (js-project-root dependency)
    "Using JS-PROJECT-ROOT, get the path to the first resolved DEPENDENCY."
    (f-join
     js-project-root
     "node_modules/"
     (eqyiel/node-find-dependency-in-package-file
      (f-join js-project-root "package.json") dependency)))

  (defun eqyiel/node-find-project-local-executable (js-project-root dependency)
    "Construct the path in JS-PROJECT-ROOT to executable DEPENDENCY."
    (condition-case nil
        (let* ((dependency-package-name
                (eqyiel/node-find-dependency-in-package-file
                 (f-join js-project-root "package.json") dependency))
               (dependency-package-file
                (f-join js-project-root "node_modules/"
                        dependency-package-name "package.json"))
               (package-file-as-string (json-read-file dependency-package-file))
               (local-project-executable
                (if (listp (cdr (assoc 'bin package-file-as-string)))
                    (cdar
                     (delq nil
                           (mapcar
                            (lambda (element)
                              (assoc
                               element
                               (cdr (assoc 'bin package-file-as-string))))
                            (if (listp dependency)
                                dependency (list (intern dependency))))))
                  (cdr (assoc 'bin package-file-as-string)))))
          (if (and local-project-executable (symbolp local-project-executable))
              (symbol-name local-project-executable)
            local-project-executable))
      (error nil)))

  (defun eqyiel/node-find-project-local-executable-path (js-project-roots dependency)
    "Make path to executable DEPENDENCY for directories in JS-PROJECT-ROOTS.

  DEPENDENCY should be a list of symbols (in which case the first one that is
  found will return) or a string.  If there are multiple JS-PROJECT-ROOTS with
  that dependency, the nearest one will be used."
    (car
     (delq
      nil
      (mapcar
       (lambda (js-project-root)
         (let* ((local-project-executable
                 (eqyiel/node-find-project-local-executable
                  js-project-root dependency)))
           (when local-project-executable
             (f-join
              (eqyiel/node-find-dependency-package-path
               js-project-root dependency)
              local-project-executable))))
       js-project-roots))))
#+END_SRC

* Packages
** ~aggressive-indent-mode~                                          :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package aggressive-indent
    :config (global-aggressive-indent-mode 1))
#+END_SRC
** ~atomic-chrome~                                                   :melpa:

This along with the ~GhostText~ Firefox addon a replacement for /It's all
text!/.

#+BEGIN_SRC emacs-lisp
  (use-package atomic-chrome
    :init (atomic-chrome-start-server)
    :demand t)
#+END_SRC

(require 'atomic-chrome)
(atomic-chrome-start-server)

** ~auth-password-store~                                             :melpa:
Note that is this is included as ~auth-source-pass~ in Emacs 26 and up.

#+BEGIN_SRC emacs-lisp
  (use-package auth-source-pass
    :after auth-source
    :init
    (progn
      ;; We could just use `auth-pass-enable' here which adds `password-store' to
      ;; `auth-sources', but I prefer to override it completely so that Emacs
      ;; never tries to read from ~/.authinfo{,.gpg} or ~/.netrc.
      (setq auth-sources '(password-store)))
        ;; Don't open in DCL mode
    :mode ("\\.com.gpg$" . fundamental-mode)
    :demand t)
#+END_SRC
** ~auto-fill-mode~                                                :builtin:
This is here just so it can be diminished.

According to the author of ~diminish.el~:

#+begin_quote
Mode names typically end in ~-mode~, but for historical reasons ~auto-fill-mode~
is named by ~auto-fill-function~.
#+end_quote

So diminish ~auto-fill-function~, not ~auto-fill-mode~.

#+BEGIN_SRC emacs-lisp
  (use-package simple :diminish auto-fill-function)
#+END_SRC

** ~beacon~                                                          :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package beacon
    :init (beacon-mode t)
    :diminish beacon-mode)
#+END_SRC

** ~buffer-move~                                                     :melpa:
#+BEGIN_SRC emacs-lisp
  ;; Caps lock and Menu keys are bound to Hyper, except on OSX and Windows which
  ;; apparently can't into Hyper.  Use fake Hyper from Karabiner-elements and
  ;; autohotkey instead, which is really M-s-S-C and Menu key respectively.
  ;;
  ;; See:
  ;; http://www.tenshu.net/p/fake-hyper-key-for-osx.html
  ;; https://github.com/tekezo/Karabiner-Elements/pull/170
  ;; https://stackoverflow.com/questions/40435980/how-to-emulate-hyper-key-in-windows-10-using-autohotkey

  (defvar eqyiel/hyper-prefix
    (cond
     ((eq system-type 'darwin)
      "M-s-S-C")
     (t
      "H")))

  ;; Bind Menu / "AppsKey" to Hyper on Windows, which is really Capslock bound to
  ;; Menu / "AppsKey" by Autohotkey.
  ;;
  ;; http://ergoemacs.org/emacs/emacs_hyper_super_keys.html
  (use-package w32-vars
    :if (eq system-type 'windows-nt)
    :config
    (progn
      ;; (setq w32-pass-lwindow-to-system nil) ; Left Windows key
      ;; (setq w32-lwindow-modifier 'super)
      ;; (setq w32-pass-rwindow-to-system nil) ; Right Windows key
      ;; (setq w32-rwindow-modifier 'super)
      (setq w32-pass-apps-to-system nil) ; Menu/App key
      (setq w32-apps-modifier 'hyper))
    :demand t)

  (use-package buffer-move
    ;; I don't think it's possible to do these guys with other hyper prefixes
    ;; because they use up all the modifiers
    :bind (("M-H-h" . buf-move-left)
           ("M-H-j" . buf-move-down)
           ("M-H-k" . buf-move-up)
           ("M-H-l" . buf-move-right))
    :config
    (progn
      ;; I don't think the :bind macro can be used here because of the value is
      ;; not static
      (bind-key (concat eqyiel/hyper-prefix "-h") 'windmove-left)
      (bind-key (concat eqyiel/hyper-prefix "-j") 'windmove-down)
      (bind-key (concat eqyiel/hyper-prefix "-k") 'windmove-up)
      (bind-key (concat eqyiel/hyper-prefix "-l") 'windmove-right)
      (bind-key (concat eqyiel/hyper-prefix "-b") 'shrink-window-horizontally)
      (bind-key (concat eqyiel/hyper-prefix "-f") 'enlarge-window-horizontally)
      (bind-key (concat eqyiel/hyper-prefix "-n") 'shrink-window)
      (bind-key (concat eqyiel/hyper-prefix "-p") 'enlarge-window))
    :demand t)
#+END_SRC

** ~c++-mode~                                                      :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package c++-mode
    :config (c-set-offset 'arglist-cont-nonempty '+)
    :bind (:map c++-mode-map ("C-c C-l" . flycheck-list-errors)))
#+END_SRC

** ~calfw~                                                           :melpa:
#+BEGIN_SRC emacs-lisp
  (defun eqyiel/open-calendar ()
    (interactive)
    (cfw:open-calendar-buffer
     :contents-sources
     (list (cfw:org-create-source))))

  (use-package calfw
    :commands (cfw:open-calendar-buffer)
    :config
    (setq calendar-mark-holidays-flag t))

  (use-package calfw-org
    :commands (cfw:org-create-source)
    :ensure calfw)
#+END_SRC

** ~circe~                                                           :melpa:

#+BEGIN_SRC emacs-lisp
  (defun eqyiel/irc ()
    "Connect to IRC."
    (interactive)
    (eqyiel/circe-setup-networks)
    (circe "freenode"))

  (defun eqyiel/circe-setup-networks ()
    (setq
     circe-network-options
     `(("freenode"
        :nick "eqyiel"
        :host "irc.freenode.net"
        :service "6697"
        :tls t
        :pass ,(password-store-get "www/irc.freenode.net")))))

  (defun eqyiel/circe-clear-passwords ()
    (if (boundp 'circe-network-options)
        (dolist (network circe-network-options)
          (plist-put (cdr network) :pass nil))))

  (defun eqyiel/circe-wait-for-authentication ()
    (setq eqyiel/circe-authentications-count
          (+ 1 eqyiel/circe-authentications-count))
    (unless (> (length circe-network-options)
               eqyiel/circe-authentications-count)
      (progn
        (eqyiel/circe-clear-passwords)
        (setq eqyiel/circe-authentications-count 0))))

  (defun eqyiel/circe-set-margin ()
    (setq right-margin-width 5))

  (defvar eqyiel/circe-authentications-count 0
    "Clear passwords after this many authentications have been seen.")

  ;; Warning: this is very dumb
  ;;
  ;; ZNC's MOTD is 25 lines.
  ;; I have two networks defined in `circe-network-options'.
  ;; So wait to see 50 notices from ZNC before enabling notifications.
  (defvar eqyiel/circe-znc-notices 0
    "How many notices have we received from ZNC?")

  (defvar eqyiel/circe-znc-motd-length 25
    "How many lines are in ZNC's MOTD?")

  (defun eqyiel/circe-znc-count-networks ()
    "Return the number of networks in `circe-network-options' multiplied by
    `eqyiel/circe-znc-motd-length', so we can know how many notices to expect before
    enabling notifications."
    (* eqyiel/circe-znc-motd-length (length circe-network-options)))

  (defun eqyiel/circe-wait-for-znc (nick userhost _command target text)
    "If this TEXT from NICK and USERHOST looks like a line of ZNC's MOTD,
    increment `eqyiel/circe-znc-notices', and enable notifications if there have
    been at least `eqyiel/circe-znc-count-networks' `eqyiel/circe-znc-notices'."
    (when (and (string-equal nick "*status")
               (string-equal userhost "znc@znc.in"))
      (setq eqyiel/circe-znc-notices (+ 1 eqyiel/circe-znc-notices))
      (message "That's %d ..." eqyiel/circe-znc-notices)
      (when (<= (eqyiel/circe-znc-count-networks) eqyiel/circe-znc-notices)
        (message "OK.")
        (advice-remove 'circe-display-NOTICE 'eqyiel/circe-wait-for-znc)
        (enable-circe-notifications))))

  (defun eqyiel/enable-circe-notifications ()
    (interactive)
    (advice-add 'circe-display-NOTICE :after 'eqyiel/circe-wait-for-znc)
    (advice-add 'circe-reconnect-all :before
                'eqyiel/disable-circe-notifications))

  (defun eqyiel/disable-circe-notifications ()
    (interactive)
    (disable-circe-notifications)
    (setq eqyiel/circe-znc-notices 0)
    (advice-add 'circe-display-NOTICE :after 'eqyiel/circe-wait-for-znc))

  (use-package circe
    :config
    (progn
      (require 'circe-chanop)
      (require 'circe-color-nicks)

      (use-package alert)

      (use-package circe-notifications
        :load-path "site-lisp/circe-notifications"
        :config
        (progn (setq circe-notifications-watch-strings
                     '("eqyiel" "versapunk" "nyarlu" "eqyiel1" "fthagn" "forcer")
                     circe-notifications-alert-style 'osx-notifier
                     circe-notifications-wait-for 30))
        :demand t)

      (use-package pass :demand t)

      (setq circe-default-quit-message
            "( ' ヮ')ノ.・ﾟ*｡・.・ﾟ*｡・.・ﾟ*｡・ヽ(ﾟДﾟ,,)ノ"
            circe-default-part-message
            "( ' ヮ')ノ.・ﾟ*｡・.・ﾟ*｡・.・ﾟ*｡・ヽ(ﾟДﾟ,,)ノ"
            circe-highlight-nick-type 'all
            circe-reduce-lurker-spam nil ;; sometimes, I want to see this
            circe-format-say "<{nick}> {body}"
            circe-format-self-say "<{nick}> {body}"
            circe-color-nicks-everywhere t
            lui-highlight-keywords '("eqyiel")
            lui-time-stamp-position 'right-margin
            lui-time-stamp-format "%H:%M"
            lui-flyspell-p t
            lui-max-buffer-size 10000
            lui-fill-column 70
            lui-fill-type 'variable
            lui-flyspell-alist '(("." "en_GB")))
      (enable-circe-color-nicks))
    :hook ((circe-server-connected-hook . enable-circe-notifications)
           (lui-mode-hook . eqyiel/circe-set-margin)
           (circe-channel-mode-hook . turn-on-flyspell)
           ;; (circe-server-connected-hook . eqyiel/circe-wait-for-authentication)
           )
    :init
    (progn
      (advice-add 'circe-reconnect-all :before 'eqyiel/circe-setup-networks)))
#+END_SRC

** ~column-enforce-mode~                                             :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package column-enforce-mode
    :hook (prog-mode-hook . column-enforce-mode)
    :diminish column-enforce-mode)
#+END_SRC

** ~company~                                                         :melpa:
#+BEGIN_SRC emacs-lisp
  (defun eqyiel/company-nixos ()
    (set (make-local-variable 'company-backends)
         '((company-nixos-options
            company-yasnippet
            company-keywords
            company-dabbrev-code
            company-files))))

  (defun eqyiel/company-elisp ()
    (set (make-local-variable 'company-backends)
         '((company-yasnippet
            company-elisp
            company-keywords
            company-dabbrev-code
            company-files))))

  (defun eqyiel/company-shell ()
    (set (make-local-variable 'company-backends)
         '((company-capf))))

  (use-package company
    :config
    (progn
      (setq company-minimum-prefix-length 1
            company-idle-delay 0
            company-dabbrev-code-everywhere t
            company-tooltip-align-annotations t)
      (add-to-list 'company-backends 'company-emoji)
      (global-company-mode))
    :init
    (progn
      (use-package company-emoji :demand t)
      (setq company-backends
            '((company-files
               company-yasnippet
               company-emoji))))
    :hook ((emacs-lisp-mode-hook . eqyiel/company-elisp)
           (nix-mode-hook . eqyiel/company-nixos)
           (shell-mode-hook . eqyiel/company-shell)
           (circe-channel-mode-hook . (lambda () (company-mode -1))))
    :bind (("M-/" . company-complete))
    :diminish company-mode)
#+END_SRC

** ~company-lsp~                                                     :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package company-lsp :after company :demand t)
#+END_SRC

** ~company-posframe~                                                :melpa:

Seems to be broken right now, throws an error like
~company-pseudo-tooltip-unless-just-one-frontend~ for any completion while this
is enabled.

#+BEGIN_SRC emacs-lisp
  ;; (use-package company-posframe
  ;;   :after company
  ;;   :demand t
  ;;   :config (company-posframe-mode t))
#+END_SRC

** ~counsel-projectile~                                              :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package counsel-projectile
    :after (projectile counsel)
    :demand t
    :bind ("C-c k" . counsel-projectile-rg))
#+END_SRC

** ~css-mode~                                                      :builtin:

#+BEGIN_SRC emacs-lisp
  (defun eqyiel/css-mode-hook ()
    (rainbow-turn-on))

  (use-package css-mode
    :config (setq css-indent-offset 2)
    :hook (css-mode-hook . eqyiel/css-mode-hook))
#+END_SRC

** ~dired~                                                         :builtin:
#+BEGIN_SRC emacs-lisp
  (defun eqyiel/dired-back-to-top ()
    "Taken from: http://whattheemacsd.com/setup-dired.el-02.html"
    (interactive)
    (beginning-of-buffer)
    (dired-next-line 4))

  (defun eqyiel/dired-jump-to-bottom ()
    "Taken from: http://whattheemacsd.com/setup-dired.el-02.html"
    (interactive)
    (end-of-buffer)
    (dired-next-line -1))

  (defun eqyiel/dired-up-directory ()
    "Reuse same dired buffer when doing `dired-up-directory'.

  See: http://www.emacswiki.org/emacs/DiredReuseDirectoryBuffer#toc1"
    (interactive)
    (find-alternate-file ".."))

  (defun eqyiel/dired-find-alternate-file-or-find-file ()
    "If the thing at point is a directory, reuse this directory buffer.  Otherwise
  do normal `dired-find-file'."
    (interactive)
    (if (directory-name-p (dired-file-name-at-point))
        (dired-find-alternate-file)
      (dired-find-file)))

  (use-package dired
    :init
    :config (setq dired-dwim-target t
                  dired-recursive-deletes 'top)
    :bind
    (:map
     dired-mode-map
     ("RET" . eqyiel/dired-find-alternate-file-or-find-file)
     ("^" . eqyiel/dired-up-directory)
     ("M-<" . eqyiel/dired-back-to-top)
     ("M->" . eqyiel/dired-jump-to-bottom)))
#+END_SRC

** ~direnv~                                                          :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package direnv
    :config (setq direnv-always-show-summary t
                  direnv-show-paths-in-summary t)
    :init (direnv-mode)
    :demand t)
#+END_SRC
** ~dockerfile-mode~                                                 :melpa:

#+BEGIN_SRC emacs-lisp
  (use-package dockerfile-mode :demand t)
#+END_SRC

** ~dtrt-indent~                                                     :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package dtrt-indent :init (dtrt-indent-mode) :demand t)
#+END_SRC

** ~emojify~                                                         :melpa:
#+BEGIN_SRC emacs-lisp
    (use-package emojify
      :config
      (setq emojify-display-style 'unicode
            emojify-composed-text-p nil))
#+END_SRC

** ~eslint-fix~                                                      :melpa:

This package is kind of crap right now because it doesn't let you set the path a
specific ~eslint~ executable, but it should be easy to fix.

#+BEGIN_SRC emacs-lisp
  (use-package eslint-fix :after js-mode js2-mode web-mode)
#+END_SRC

** ~expand-region~                                                   :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package expand-region
    :chords ((("jk" . er/expand-region)
              ("kj" . er/expand-region)))
    :bind ("C-=" . er/expand-region)
    :demand t)
#+END_SRC

** ~fasd~                                                            :melpa:

#+BEGIN_SRC emacs-lisp
  (use-package fasd
    :bind ("C-z" . fasd-find-file)
    :init (global-fasd-mode 1))
#+END_SRC

** ~flow-minor-mode~                                                 :melpa:
#+BEGIN_SRC emacs-lisp
  (defun eqyiel/flow-minor-mode-hook ()
    (let ((js-project-roots (eqyiel/node-find-js-project-roots)))
      (when (eqyiel/node-should-use-package-p js-project-roots '(flow flow-bin))
        (let ((local-project-flow-executable
               (eqyiel/node-find-project-local-executable-path
                js-project-roots '(flow flow-bin))))
          (when local-project-flow-executable
            (set (make-local-variable 'flow-minor-default-binary)
                 local-project-flow-executable))))))

  (use-package flow-minor-mode
    :hook (flow-minor-mode-hook . eqyiel/flow-minor-mode-hook)
    :demand t)
#+END_SRC

** ~flycheck~                                                        :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package flycheck
    :config
    (progn
      (setq-default
       flycheck-disabled-checkers
       (append flycheck-disabled-checkers
               '(handlebars html-tidy javascript-jshint javascript-jscs php)))
      (setq flycheck-gcc-pedantic t
            flycheck-display-errors-delay 0.1
            flycheck-error-list-minimum-level 'warning))
    :init
    (progn
      (use-package web-mode)
      (global-flycheck-mode)
      (flycheck-add-mode 'javascript-eslint 'web-mode)
      (setq flycheck-eslintrc ".eslintrc.json"))
    :bind ("C-c C-l" . flycheck-list-errors)
    :demand t
    :diminish flycheck-mode)
#+END_SRC

** ~flycheck-flow~                                                   :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package flycheck-flow
    :after flycheck
    :config
    (progn
      ;; if using flow-coverage
      ;; (flycheck-add-next-checker 'javascript-flow '(t . javascript-flow-coverage))
      ;; (flycheck-add-next-checker 'javascript-flow-coverage '(t . javascript-eslint))
      ;; if just using flow
      (flycheck-add-next-checker 'javascript-flow '(t . javascript-eslint))))
#+END_SRC

** ~geben~                                                           :melpa:

PHP debugger thing.

#+BEGIN_SRC emacs-lisp
  (use-package geben)
#+END_SRC

** ~gist~                                                            :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package gist
    :config
    (let ((gh-vals (cdar gh-profile-alist)))
      (setf
       gh-vals (plist-put gh-vals :username "eqyiel")
       gh-vals
       (plist-put
        gh-vals
        :token (password-store-get "tokens/github.com/eqyiel/gist")))))
#+END_SRC

** ~git-link~                                                        :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package git-link)
#+END_SRC

** ~go-mode~                                                         :melpa:

#+BEGIN_SRC emacs-lisp
  (use-package go-mode
    :hook (before-save-hook . gofmt-before-save))
#+END_SRC

** ~google-c-style~                                                  :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package google-c-style
    :hook (c-mode-common-hook . google-set-c-style))
#+END_SRC

** ~google-translate~                                                :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package google-translate)
#+END_SRC

** ~graphql-mode~                                                    :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package graphql-mode)
#+END_SRC

** ~groovy-mode~                                                     :melpa:

This is pretty much exclusively for the sake of editing ~build.gradle~ files.

#+BEGIN_SRC emacs-lisp
  (use-package groovy-mode
    :mode ("\\.gradle\\'" . groovy-mode))
#+END_SRC

** ~help-at-pt~                                                    :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package help-at-pt
    :config
    (setq help-at-pt-timer-delay 0.1
          help-at-pt-display-when-idle t)
    :demand t)
#+END_SRC

** ~highlight-indentation~                                           :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package highlight-indentation
    :hook (prog-mode-hook . highlight-indentation-mode)
    :diminish highlight-indentation-mode)
#+END_SRC

** ~html-mode~                                                     :builtin:
#+BEGIN_SRC emacs-lisp
  (defun eqyiel/html-mode-hook ()
    (rainbow-turn-on))

  (use-package html-mode
    :hook (html-mode-hook . eqyiel/html-mode-hook))
#+END_SRC

** ~ibuffer~                                                       :builtin:

#+BEGIN_SRC emacs-lisp
  (use-package ibuffer
    :bind (("C-x C-b" . ibuffer)))
#+END_SRC

** ~ibuffer-projectile~                                              :melpa:

#+BEGIN_SRC emacs-lisp
  (use-package ibuffer-projectile
    :hook
    (ibuffer-hook . (lambda ()
      (ibuffer-projectile-set-filter-groups)
      (unless (eq ibuffer-sorting-mode 'alphabetic)
        (ibuffer-do-sort-by-alphabetic)))))
#+END_SRC

** ~info~                                                          :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package info
    :hook
    (Info-mode-hook
     . (lambda ()
         (setq Info-additional-directory-list Info-default-directory-list)))
    :bind
    (:map Info-mode-map
          ;; Let me use S-SPC to scroll backwards in info mode.
          ("S-SPC" . Info-scroll-down)))
#+END_SRC

** ~isearch~                                                       :builtin:

I prefer ~swiper~ on ~C-s~ but since it's line based, there are some things it
can't do well (like searching for a substring in a long command in
`comint-mode`) so rebind ~isearch~ to a convenient key.

#+BEGIN_SRC emacs-lisp
  (use-package isearch
    :bind (("C-c C-M-s" . isearch-forward)
           ("C-c C-M-r" . isearch-backward)))
#+END_SRC

** ~ispell~                                                        :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package ispell
    :hook (((text-mode-hook org-mode-hook) . turn-on-flyspell)
           (prog-mode-hook . flyspell-prog-mode))
    :config
    (when (and (bound-and-true-p ispell-program-name)
               (executable-find ispell-program-name))
      (setq ispell-dictionary "english"
            ispell-personal-dictionary "~/.aspell.en.pws"
            ispell-aspell-data-dir
            (lambda ()
              (let ((nix-aspell-dict-dir "/run/current-system/sw/lib/aspell"))
                (when (file-exists-p (directory-file-name nix-aspell-dict-dir))
                  nix-aspell-dict-dir))))))
#+END_SRC

** ~js-mode~                                                       :builtin:

I don't really use this, but configure it in case I ever activate it by accident
instead of ~js2-mode~ or ~web-mode~.

#+BEGIN_SRC emacs-lisp
  (use-package js-mode
    :config
    (progn
      (setq js-indent-level 2)
      ;; https://emacs.stackexchange.com/questions/22044/treat-shebang-as-a-comment
      (modify-syntax-entry ?# ". 1" js-mode-syntax-table)
      (modify-syntax-entry ?! ". 2b" js-mode-syntax-table)))
#+END_SRC

** ~java-mode~                                                     :builtin:

#+BEGIN_SRC emacs-lisp
  (defun eqyiel/company-java-mode-hook ()
    (set (make-local-variable 'company-backends)
         '((company-files
            company-lsp
            company-keywords))))

  (use-package java-mode
    :hook (java-mode-hook . eqyiel/company-java-mode-hook))
#+END_SRC

** ~js-mode~                                                       :builtin:
#+BEGIN_SRC emacs-lisp
  (defun eqyiel/pseudo-gml-mode-hook ()
    "For lack of a better mode for game-maker stuff, use `js-mode'."
    (let
        ((extension (file-name-extension (buffer-file-name))))
      (when
          (or
           (string-equal extension "yy")
           (string-equal extension "yyp"))
        ;; Game Maker strips the final newline when writing to these files.
        ;; Prevent Emacs from adding one automatically upon save for cleaner
        ;; diffs.
        (setq-local require-final-newline nil))))

  (use-package js-mode
    :config (setq js-indent-level 2)
    :hook (js-mode-hook . eqyiel/pseudo-gml-mode-hook)
    :mode (("\\.gml\\'" . js-mode)
           ("\\.yy\\'" . js-mode)
           ("\\.yyp\\'" . js-mode)))
#+END_SRC

** ~js2-mode~                                                        :melpa:
#+BEGIN_SRC emacs-lisp
  (defun eqyiel/toggle-js2-mode-to-web-mode ()
    (interactive)
    (web-mode))

  (defun eqyiel/company-js2-mode-hook ()
    (set (make-local-variable 'company-backends)
         '((company-files
            company-lsp
            company-keywords))))

  (defun eqyiel/js2-mode-hook ()
    (progn
      (setq-local js-switch-indent-offset 2)
      (setq-local js2-basic-offset 2)
      (setq-local js2-concat-multiline-strings nil)
      (setq-local js2-concat-multiline-strings t)
      (setq-local js2-highlight-level 3)
      (setq-local js2-idle-timer-delay 3)  ;; wait until I'm actually idle
      (setq-local js2-include-node-externs t)
      (setq-local js2-mode-show-parse-errors nil)
      (setq-local js2-mode-show-strict-warnings nil)
      (setq-local js2-strict-cond-assign-warning nil)
      (setq-local js2-strict-inconsistent-return-warning nil)
      (setq-local js2-strict-missing-semi-warning nil)
      (setq-local js2-strict-trailing-comma-warning nil)
      (setq-local js2-strict-var-hides-function-arg-warning nil)
      (setq-local js2-strict-var-redeclaration-warning nil)
      (activate-flow-js2-mode)
      (rainbow-mode)))

  (use-package js2-mode
    :config
    (progn
      (use-package web-mode)
      (use-package flycheck)
      (use-package company)

      (use-package flow-js2-mode
        :load-path "site-lisp/flow-js2-mode"
        :config (use-package flow-minor-mode :demand t)
        :demand t)

      ;; https://emacs.stackexchange.com/questions/22044/treat-shebang-as-a-comment
      (modify-syntax-entry ?# ". 1" js2-mode-syntax-table)
      (modify-syntax-entry ?! ". 2b" js2-mode-syntax-table))
    :hook ((js2-mode-hook . eqyiel/company-js2-mode-hook)
           ((js2-mode-hook js2-jsx2-mode-hook) . eqyiel/js2-mode-hook))
    :bind
    (:map js2-mode-map
          ("C-M-s-\"" . eqyiel/toggle-js2-mode-to-web-mode)
          ("H-'" . eqyiel/toggle-js2-mode-to-web-mode)))
#+END_SRC

** ~json-mode~                                                       :melpa:
This package adds itself to ~auto-mode-alist~.

#+BEGIN_SRC emacs-lisp
  (defun eqyiel/json-mode-hook ()
      (setq js-indent-level 2
            json-reformat:indent-width 2))

  (defun eqyiel/json-run-jq (query &optional arg)
    "Run jq(1) on current buffer.
  With prefix argument \\[universal-argument] replace the buffer
  with the result of running jq(1)."
    (interactive "sjq query: \nP")
    (shell-command-on-region
     (point-min) (point-max)
     (concat "jq " (shell-quote-argument query))
     (when arg (current-buffer))
     (when arg t)))

  (defun eqyiel/json-minify ()
    "Minify the json at point."
    (interactive)
    (goto-char (point-min))
    (let* ((p (point))
           (json (json-read)))
      (delete-region p (point))
      (save-excursion
        (insert (json-encode json)))))

  (use-package json-mode
    :bind (:map json-mode-map
                ("C-c C-c" . eqyiel/json-run-jq)
                ("C-c C-m" . eqyiel/json-minify))
    :mode (("\\.eslintrc.*$" . json-mode)
           ("\\.babelrc$" . json-mode))
    :hook (json-mode-hook . eqyiel/json-mode-hook))
#+END_SRC

** ~kubernetes-mode~                                                 :melpa:

#+BEGIN_SRC emacs-lisp
  (defvar eqyiel/kubernetes-namespaces '("minikube"))

  (defun eqyiel/kubernetes-kubectl-around (orig-fun &rest orig-args)
    "Don't have permission to list all namespaces.  This is a hack to use a
  predetermined list of namespaces, with `eqyiel/kubernetes-namespaces' set in
  dir-locals."
    (let ((type (nth 1 (nth 2 orig-args))))
      (if (and (stringp type) (s-equals-p type "namespaces"))
          (let* ((on-success (nth 3 orig-args))
                 (buf (generate-new-buffer "kubectl"))
                 (command `("printf"
                            "%s"
                            ,(json-encode
                              `(( apiVersion . "v1" )
                                (items . ,(mapcar (lambda (namespace)
                                                   `((metadata . ((name . ,namespace))))) eqyiel/kubernetes-namespaces))
                                (kind . "List")
                                (metadata . ((resourceVersion . "")
                                             (selfLink . ""))))))))

                 (make-process
                   :name "kubectl"
                   :buffer buf
                   :command command
                   :noquery t
                   :sentinel
                   (lambda (process &rest args)
                     (funcall on-success buf)
                     (kubernetes-process-kill-quietly process))))
        (apply orig-fun orig-args))))

  (advice-add 'kubernetes-kubectl :around 'eqyiel/kubernetes-kubectl-around)

  (defun eqyiel/kubernetes-overview (orig-fun &rest orig-args)
    "Prevent use of `kubernetes-overview' in contexts where
  `eqyiel/kubernetes-namespaces' is nil."
    (if eqyiel/kubernetes-namespaces
        (apply orig-fun orig-args)
      (message "`eqyiel/kubernetes-namespaces' is nil, doing nothing")))

  (advice-add 'kubernetes-overview :around 'eqyiel/kubernetes-overview)

  (use-package kubernetes
    :config
    ;; Emacs hangs if this updates too frequently.  Use "g" to update.
    (setq kubernetes-poll-frequency 3600
          kubernetes-redraw-frequency 3600)
    :commands (kubernetes-overview))
#+END_SRC

** ~legalese~                                                        :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package legalese)
#+END_SRC

** ~lsp-mode~                                                        :melpa:

#+BEGIN_SRC emacs-lisp
  (use-package lsp-ui)

  (use-package lsp-mode
    :requires flycheck nix-buffer
    :demand t
    :config (require 'lsp-ui))

  (defun eqyiel/lsp-javascript-flow-find-executable ()
    (let ((js-project-roots (eqyiel/node-find-js-project-roots)))
      (when (eqyiel/node-should-use-package-p
             js-project-roots '(flow-language-server))
        (eqyiel/node-find-project-local-executable-path
         js-project-roots '(flow-language-server)))))

  (defun eqyiel/lsp-javascript-flow-enable-maybe ()
    (interactive)
    (let ((flow-server-executable (eqyiel/lsp-javascript-flow-find-executable)))
      (when (flow-minor-tag-present-p)
        (if flow-server-executable
            (progn
              ;; Run nix-buffer if necessary to put things like node in PATH
              (eqyiel/nix-buffer-find-file-hook)
              (setq-local lsp-clients-flow-server flow-server-executable))
          (message "// @flow tag present, but no flow-language-server found in project")))))

  (use-package
    flow-minor-mode
    :hook ((js-mode-hook js2-mode-hook rjsx-mode-hook)
           . eqyiel/lsp-javascript-flow-enable-maybe)
    :demand t)

  (defun eqyiel/lsp-java-enable-maybe ()
    (interactive)
    (require 'lsp-java)
    (progn
      (eqyiel/nix-buffer-find-file-hook)
      ;; Unfortunately this doesn't work because eclipse wants to write to the
      ;; configuration file at /nix/store/.../share/java/config_linux/config.ini. :(
      ;;
      ;; It may work if that file can get copied elsewhere (a temp directory), and
      ;; `lsp-java--locate-server-config' is overridden with the copied file path.
      ;;
      ;; (let ((get-lsp-java-server-install-dir-executable
      ;;        (executable-find "get-lsp-java-server-install-dir")))
      ;;   (when get-lsp-java-server-install-dir-executable
      ;;     (setq-local
      ;;      lsp-java-server-install-dir
      ;;      (shell-command-to-string get-lsp-java-server-install-dir-executable))))
      (lsp-java-enable)
      (lsp-ui-mode)))

  (use-package lsp-java
    :hook (java-mode-hook . eqyiel/lsp-java-enable-maybe))
#+END_SRC

** ~magit~                                                           :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package magit
    :init (use-package ivy)
    :config
    (progn
      (setq
       magit-completing-read-function 'ivy-completing-read
       magit-save-repository-buffers 'dontask))
    :bind
    (("<f8>" . magit-status)
     ("C-x g" . magit-status)))
#+END_SRC

** ~markdown-mode~                                                   :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package markdown-mode
    :config
    (progn
      (setq markdown-command "pandoc -f markdown -t html -s --mathjax --highlight-style=pygments"))
    :mode (("\\.markdown\\'" . markdown-mode)
           ("\\.md\\'" . markdown-mode)))
#+END_SRC

** ~matrix-client~                                                   :melpa:
This is no longer on MELPA unfortunately.  This is how I found out that MELPA
will be contacted on each startup if the package you want to install has
vanished.

#+BEGIN_SRC emacs-lisp
  ;; (use-package matrix-client
  ;;   :init
  ;;   (defun eqyiel/launch-matrix-client ()
  ;;     (interactive)
  ;;     (matrix-client "eqyiel"))
  ;;   :config
  ;;   (setq matrix-homeserver-base-url "https://matrix.rkm.id.au"))
#+END_SRC

** ~message~                                                       :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package message
    :init
    (setq
     message-directory eqyiel/mail-directory
     message-send-mail-function 'message-send-mail-with-sendmail
     message-cite-function 'message-cite-original-without-signature
     message-default-charset 'utf-8
     message-default-mail-headers "Cc: \nBcc: \n"
     message-from-style 'angles
     message-generate-headers-first t
     message-kill-buffer-on-exit t))
#+END_SRC

** ~mu4e~                                                           :system:

Check out this for tips: https://vxlabs.com/2017/02/07/mu4e-0-9-18-e-mailing-with-emacs-now-even-better/

#+BEGIN_SRC emacs-lisp
  (defun eqyiel/mu4e-any-message-field-at-point (msg header)
    "Quick & dirty way to get an arbitrary header HEADER from MSG.

  Requires the 'formail' tool from procmail.

  This is a thing because `mu4e-message-field' doesn't support getting arbitrary
  fields, such as X-GitHub-Sender."
     (replace-regexp-in-string "\n$" ""
       (shell-command-to-string
         (concat "formail -x " header " -c < "
           (shell-quote-argument (mu4e-message-field msg :path))))))

  (defun eqyiel/mu4e-get-x-github-sender (msg)
    "Get the X-Github-Sender field from a MSG"
    (s-trim (eqyiel/mu4e-any-message-field-at-point msg "X-GitHub-Sender")))

  (defun eqyiel/mu4e-refile-sent-items-to-sent-folder (sent-folder refile-folder)
    "When I'm refiling a thread I usually just mash the r key.

  Because I have `mu4e-headers-include-related' that means that mesages from me
  will also be refiled.  I'd prefer if they didn't get moved from
  `mu4e-sent-folder' to `mu4e-refile-folder', so this function returns a function
  that a suitable setting for `mu4e-refile-folder' and avoids refiling from
  SENT-FOLDER into REFILE-FOLDER."
    `(lambda (message)
      (if (s-ends-with-p "Sent" (mu4e-message-field msg :maildir) t)
          ,sent-folder
        ,refile-folder)))

  (use-package mu4e
    :config
    (progn
      (use-package org-mime :demand t)
      (defun eqyiel/htmlize-and-send ()
        "When in an org-mu4e-compose-org-mode message, htmlize and send it."
        (interactive)
        (when (member 'org~mu4e-mime-switch-headers-or-body post-command-hook)
          (org-mime-htmlize)
          (org-mu4e-compose-org-mode)
          (mu4e-compose-mode)
          (message-send-and-exit)))

      ;; It's not possible to use use-package's :hook for this right now because
      ;; the "local" argument is not supported there.
      ;; https://github.com/jwiegley/use-package/issues/668
      (add-hook 'org-ctrl-c-ctrl-c-hook 'eqyiel/htmlize-and-send t)
      (setq
       mu4e-get-mail-command
       (if (eq system-type 'darwin)
           "mbsync r-maher@mercari.com"
         "mbsync ruben@maher.fyi"))
      (use-package org-mu4e :config (setq org-mu4e-convert-to-html t))
      (use-package simple :config (setq mail-user-agent 'mu4e-user-agent))
      (setq mu4e-change-filenames-when-moving t) ;; needed for mbsync?
      (setq mu4e-maildir eqyiel/mail-directory)
      (setq mu4e-sent-folder eqyiel/mail-directory)
      (setq mu4e-compose-format-flowed t) ;; https://www.ietf.org/rfc/rfc3676.txt
      (setq mu4e-use-fancy-chars nil)
      (setq mu4e-attachment-dir eqyiel/downloads-directory)
      (setq mu4e-completing-read-function 'completing-read)
      (setq mu4e-index-cleanup t)
      (setq mu4e-headers-include-related t) ;; needed for proper threading
      (setq mu4e-headers-results-limit 1500)
      (setq mu4e-compose-keep-self-cc t)
      (setq send-mail-function 'message-send-mail-with-sendmail
            sendmail-program "msmtpq")  ; from gnus config
      (setq mu4e-view-show-addresses t)
      (setq
       mu4e-header-info-custom
       '((:x-github-sender .
          (:name "X-Github-Sender"
                 :shortname "GitHub Handle"
                 :help "From field with GitHub handle if it exists"
                 :function eqyiel/mu4e-get-x-github-sender))))
      (setq mu4e-view-fields
            '(:from
              :x-github-sender
              :to
              :cc
              :subject
              :flags
              :date
              :maildir
              :mailing-list
              :tags
              :attachments
              :signature
              :decryption))
      (setq mu4e-view-actions
            '(("capture message" . mu4e-action-capture-message)
              ("view as pdf" . mu4e-action-view-as-pdf)
              ("show this thread" . mu4e-action-show-thread)
              ("View in browser" . mu4e-action-view-in-browser)
              ("Eww view" .
               (lambda (msg)
                 (eww-browse-url
                  (concat
                   "file://"
                   (mu4e~write-body-to-html msg)))))))
      (setq mu4e-contexts
            `(,(make-mu4e-context
                :name "m ruben@maher.fyi"
                :match-func
                (lambda (msg)
                  (when msg
                    (string=
                     (mu4e-message-field msg :maildir)
                     "/ruben@maher.fyi")))
                :vars `((user-mail-address . "ruben@maher.fyi")
                        (mu4e-sent-folder . "/ruben@maher.fyi/Sent")
                        (mu4e-refile-folder
                         . ,(eqyiel/mu4e-refile-sent-items-to-sent-folder
                            "/ruben@maher.fyi/Sent"
                            "/ruben@maher.fyi/Archive"))
                        (mu4e-drafts-folder . "/ruben@maher.fyi/Drafts")
                        (mu4e-trash-folder . "/ruben@maher.fyi/Trash")
                        (mu4e-sent-messages-behavior . sent)
                        (mu4e-compose-crypto-reply-policy . sign-and-encrypt)
                        (mu4e-maildir-shortcuts
                         . (("/ruben@maher.fyi/Inbox"   . ?i)
                            ("/ruben@maher.fyi/Archive" . ?a)
                            ("/ruben@maher.fyi/GitHub"  . ?g)
                            ("/ruben@maher.fyi/Lists"   . ?l)
                            ("/ruben@maher.fyi/Trash"   . ?t)
                            ("/ruben@maher.fyi/Sent"    . ?s)))))
              ,(make-mu4e-context
                :name "r@rkm.id.au"
                :vars `((user-mail-address . "r@rkm.id.au")
                        (mu4e-sent-folder . "/ruben@maher.fyi/Sent")
                        (mu4e-refile-folder
                         . ,(eqyiel/mu4e-refile-sent-items-to-sent-folder
                            "/ruben@maher.fyi/Sent"
                            "/ruben@maher.fyi/Archive"))
                        (mu4e-drafts-folder . "/ruben@maher.fyi/Drafts")
                        (mu4e-trash-folder . "/ruben@maher.fyi/Trash")
                        (mu4e-sent-messages-behavior . sent)
                        (mu4e-compose-crypto-reply-policy . sign-and-encrypt)
                        (mu4e-maildir-shortcuts
                         . (("/ruben@maher.fyi/Inbox"   . ?i)
                            ("/ruben@maher.fyi/Archive" . ?a)
                            ("/ruben@maher.fyi/GitHub"  . ?g)
                            ("/ruben@maher.fyi/Lists"   . ?l)
                            ("/ruben@maher.fyi/Trash"   . ?t)
                            ("/ruben@maher.fyi/Sent"    . ?s)))))
              ,(make-mu4e-context
                :name "flinders.edu.au"
                :match-func
                (lambda (msg)
                  (when msg
                    (string=
                     (mu4e-message-field msg :maildir)
                     "/mahe0054@flinders.edu.au")))
                :vars `((user-mail-address . "mahe0054@flinders.edu.au")
                        (mu4e-sent-folder . "/ruben@maher.fyi/Sent")
                        (mu4e-refile-folder
                         . ,(eqyiel/mu4e-refile-sent-items-to-sent-folder
                            "/ruben@maher.fyi/Sent"
                            "/ruben@maher.fyi/Archive"))
                        (mu4e-drafts-folder . "/ruben@maher.fyi/Drafts")
                        (mu4e-trash-folder . "/ruben@maher.fyi/Trash")
                        (mu4e-sent-messages-behavior . sent)
                        (mu4e-compose-crypto-reply-policy . sign-and-encrypt)
                        (mu4e-maildir-shortcuts
                         . (("/ruben@maher.fyi/Inbox"   . ?i)
                            ("/ruben@maher.fyi/Archive" . ?a)
                            ("/ruben@maher.fyi/GitHub"  . ?g)
                            ("/ruben@maher.fyi/Lists"   . ?l)
                            ("/ruben@maher.fyi/Trash"   . ?t)
                            ("/ruben@maher.fyi/Sent"    . ?s)))))
              ,(make-mu4e-context
                :name "kouzoh"
                :match-func
                (lambda (msg)
                  (when msg
                    (string= (mu4e-message-field msg :maildir)
                             "/r-maher@mercari.com")))
                :vars `((user-mail-address . "r-maher@mercari.com")
                        (mu4e-sent-folder . "/r-maher@mercari.com/Sent Mail")
                        (mu4e-refile-folder
                         . ,(eqyiel/mu4e-refile-sent-items-to-sent-folder
                            "/r-maher@mercari.com/Sent Mail"
                            "/r-maher@mercari.com/All Mail"))
                        (mu4e-drafts-folder . "/r-maher@mercari.com/Drafts")
                        (mu4e-trash-folder . "/r-maher@mercari.com/Trash")
                        ;; GMail already takes care of keeping copies in the sent
                        ;; folder
                        (mu4e-sent-messages-behavior . delete)
                        (mu4e-maildir-shortcuts
                         . (("/r-maher@mercari.com/Inbox"  . ?i)
                            ("/r-maher@mercari.com/Drafts"    . ?d)
                            ("/r-maher@mercari.com/Trash"     . ?t)
                            ("/r-maher@mercari.com/Sent Mail" . ?s))))))))
    :init (use-package org-mu4e :demand t)
    :bind (("<f4>" . mu4e)
           ("C-c 4" . mu4e)
           ("C-x m" . mu4e-compose-new))
    :demand t
    :load-path "/run/current-system/sw/share/emacs/site-lisp/mu4e")
#+END_SRC

** ~mule~                                                          :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package mule
    :init (setq default-input-method "japanese")
    :demand t)
#+END_SRC

** ~multiple-cursors~                                                :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package multiple-cursors
    :config
    (setq
     mc/always-run-for-all t
     mc/always-repeat-command t
     mc/list-file (concat eqyiel/xdg-cache-home "/emacs/mc-lists.el"))
    :bind (("C-M-*" . mc/edit-lines)
           ("C->" . mc/mark-next-like-this)
           ("C-<" . mc/mark-previous-like-this)
           ("C-8" . mc/mark-all-like-this)
           ("C-*" . mc/mark-all-like-this)))
#+END_SRC

** ~nix-buffer~                                                      :melpa:
#+BEGIN_SRC emacs-lisp
  (defun eqyiel/nix-buffer-find-file-hook ()
    "Avoid using nix-buffer on slow connections (over tramp, for instance)."
    (when (not (file-remote-p (buffer-file-name)))
      (nix-buffer)))

  (use-package nix-buffer
    :config
    (nix-buffer-update-directory-name
     (f-join eqyiel/xdg-cache-home "emacs/nix-buffer"))
    :hook (find-file-hook . eqyiel/nix-buffer-find-file-hook)
    :demand t)
#+END_SRC

** ~nix-mode~                                                        :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package nix-mode
    :commands (nix-mode nix-repl-show nix-format-buffer)
    :mode ("\\.nix\\'" . nix-mode)
    :init (progn (defalias 'nix-format-buffer 'nixpkgs-fmt-buffer)))
#+END_SRC

** ~nix-shell~                                                       :melpa:

#+BEGIN_SRC emacs-lisp
  (use-package nix-shell
    :commands nix-shell)
#+END_SRC

** ~nix-update~                                                      :melpa:

#+BEGIN_SRC emacs-lisp
  (use-package nix-update
    :commands nix-update-fetch)
#+END_SRC

** ~nixos-options~                                                   :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package company-nixos-options
    :after (company nix-mode))

  (use-package nixos-options
    :after nix-mode)
#+END_SRC

** ~nixpkgs-fmt~                                                     :melpa:

#+BEGIN_SRC emacs-lisp
  (use-package nixpkgs-fmt
    :hook (nix-mode-hook . nixpkgs-fmt-on-save-mode)
    :after nix-mode
    :commands nixpkgs-fmt-on-save-mode)
#+END_SRC

** ~no-littering~                                                    :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package no-littering)
#+END_SRC

** ~nodejs-repl~                                                     :melpa:
#+BEGIN_SRC emacs-lisp
  (defun eqyiel/nodejs-repl-nix-buffer-around (orig-fun &rest args)
    "Allow ORIG-FUN to find `nodejs-repl-command' given by `nix-buffer'."
    (let ((nodejs-repl-command (executable-find "node")))
      (funcall orig-fun)))

  (use-package nodejs-repl
    :config (advice-add 'nodejs-repl :around 'eqyiel/nodejs-repl-nix-buffer-around))
#+END_SRC

** ~omnisharp~                                                       :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package omnisharp
    :hook (csharp-mode-hook . omnisharp-mode))
#+END_SRC

** ~org-caldav~                                                      :melpa:

The goal here is to sync everything that has the tag ~:shared:~.  I would prefer
to do ~(setq org-caldav-select-tags '("shared"))~ but it ~ox-icalendar.el~ seems
to ignore it.

I also tried setting ~(setq org-caldav-skip-conditions '(notregexp ":shared:"))~
but the trouble with that is that ~org-agenda-skip-if~ uses boolean ~OR~ to
combine the results from different tests, and I also want to exclude items with
todo keywords ~DONE~.  Negating a regular expression is [[https://stackoverflow.com/questions/2217928/][difficult in Emacs lisp]],
so the only thing left to do is advise ~org-caldav-skip-function~ so that I can
use ~AND~ instead of ~OR~.

#+BEGIN_SRC emacs-lisp
  (use-package org-caldav
    :config
    (progn
      (advice-add
       'org-caldav-skip-function :override
       (lambda (backend)
         (when (eq backend 'icalendar)
           (org-map-entries
            (lambda ()
              (let ((pt (and
                         (org-agenda-skip-if 'notregexp '(":shared:"))
                         (org-agenda-skip-if 'todo '("DONE")))))
                (when pt
                  (delete-region (point) pt))))))))

      (setq
       org-caldav-calendar-id "org"
       org-caldav-delete-org-entries 'ask
       org-caldav-files (directory-files eqyiel/org-directory t "\\.org$")
       org-caldav-inbox (concat eqyiel/org-directory "/org-caldav-inbox.org")
       org-caldav-save-directory (concat eqyiel/xdg-cache-home "/emacs")
       org-caldav-url "https://cloud.maher.fyi/remote.php/dav/calendars/eqyiel"
       org-icalendar-include-todo t
       org-caldav-skip-conditions t ; must be truthy to run `org-caldav-skip-function'
       org-icalendar-timezone eqyiel/timezone
       org-icalendar-use-scheduled '(event-if-todo))))
#+END_SRC

** ~org-cliplink~                                                    :melpa:
A tool to insert a link in the clipboard as an ~org-mode~ link at point.

#+BEGIN_SRC emacs-lisp
(use-package org-cliplink :bind (("H-i" . org-cliplink)))
#+END_SRC

** ~org-download~                                                    :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package org-download
    :config (setq org-download-method 'attach))
#+END_SRC

** ~org-indent-mode~                                               :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package org-indent
    :load-path "site-lisp/org-mode"
    :diminish org-indent-mode
    :demand t)
#+END_SRC

** ~org-mode~                                                      :builtin:

#+BEGIN_SRC emacs-lisp
  (defconst eqyiel/org-iso8601-date-format "[%FT%T%z]")

  (defun eqyiel/org-insert-created-property ()
    "Insert CREATED property at point in ISO 8601 format."
    (interactive)
    (org-set-property
     "CREATED"
     (format-time-string eqyiel/org-iso8601-date-format (current-time))))

  (defun eqyiel/org-iso8601ify-date-string (date)
    "Try to change DATE to ISO 8601 format."
    (format-time-string eqyiel/org-iso8601-date-format (date-to-time date)))

  (defun eqyiel/org-agenda-skip-non-toplevel-headings ()
    "`org-agenda-skip-function' that skips over all but top level headings."
    (unless (= 1 (nth 1 (org-heading-components)))
      (org-end-of-subtree)))

  (defun eqyiel/org-iso8601ify-created-properties (query)
    "Change CREATED timestamps to ISO 8601 format for entries matching QUERY."
    (org-map-entries
     (lambda ()
       (let ((target-property "CREATED"))
         (org-entry-put
          (point)
          target-property
          (eqyiel/org-iso8601ify-date-string
           (org-entry-get (point) target-property)))))
     query 'agenda
     'eqyiel/org-agenda-skip-non-toplevel-headings))

  (defun eqyiel/org-prev-visible-heading (arg)
    "Move to the previous visible heading.

    This function wraps `outline-prev-visible-heading' with
    `org-with-limited-levels' in order to skip over inline tasks and
    respect customization of `org-odd-levels-only'."
    (interactive "p")
    (org-with-limited-levels
     (outline-previous-visible-heading arg)))

  (defun eqyiel/create-org-file-path (filename)
    (format "%s/%s" eqyiel/org-directory filename))

  (defun eqyiel/maybe-create-org-agenda-file (filename)
    (let ((target
           (eqyiel/create-org-file-path filename)))
      (progn (unless (and (file-exists-p target)
                          (file-readable-p target)
                          (file-writable-p target))
               (with-temp-buffer (write-file target)))
             target)))

  (defun eqyiel/org-capture-prepare-finalize-hook ()
    "Quality of life hack for for `org-capture' buffers.

  This allows the use of `org-capture-finalize' without having the point on the
  heading."
    (org-back-to-heading))

  (defun eqyiel/org-capture-before-finalize-hook ()
    "Add slugified category to tags.
  That is: if a file has a \"#+CATEGORY: test\" header or a subtree has a
  CATEGORY in its properties drawer, set the tags of the captured item to
  those categories (slugified)."
    (let ((tags
           (delq nil
                 (cons
                  (delq "" (list (eqyiel/slugify (org-get-category) "_")))
                  (org-get-tags)))))
      (when tags
        (progn
          (org-set-tags-to tags)
          (org-align-all-tags)))))

  (defun eqyiel/org-mode-hook ()
    (progn
      (org-indent-mode t)
      (turn-on-auto-fill)))

  (defun eqyiel/org-remove-deadline ()
    "Remove deadline from the entry at point."
    (interactive)
    (execute-extended-command '(4) "org-deadline"))
  (defun eqyiel/org-md-export-to-markdown (&optional async subtreep visible-only)
        "Export current buffer to a Markdown file.

  If narrowing is active in the current buffer, only export its
  narrowed part.

  If a region is active, export that region.

  A non-nil optional argument ASYNC means the process should happen
  asynchronously.  The resulting file should be accessible through
  the `org-export-stack' interface.

  When optional argument SUBTREEP is non-nil, export the sub-tree
  at point, extracting information from the headline properties
  first.

  When optional argument VISIBLE-ONLY is non-nil, don't export
  contents of hidden elements.

  Return output file's name."
        (interactive)
        (let ((outfile (org-export-output-file-name ".md" subtreep)))
          (org-export-to-file 'md outfile async subtreep visible-only)))

      (defun eqyiel/org-md-publish-to-md (plist filename pub-dir)
        "Publish an org file to Markdown.

  FILENAME is the filename of the Org file to be published.  PLIST
  is the property list for the given project.  PUB-DIR is the
  publishing directory.

  Return output file name."
        (org-publish-org-to 'rkm-md filename ".md" plist pub-dir))

  (defun eqyiel/org-md-headline (headline contents info)
    "Transcode HEADLINE element into Markdown format.
  CONTENTS is the headline contents.  INFO is a plist used as
  a communication channel."
    (unless (org-element-property :footnote-section-p headline)
      (let* ((level (org-export-get-relative-level headline info))
             (title (org-export-data (org-element-property :title headline) info))
             (todo (and (plist-get info :with-todo-keywords)
                        (let ((todo (org-element-property :todo-keyword
                                                          headline)))
                          (and todo (concat (org-export-data todo info) " ")))))
             (tags (and (plist-get info :with-tags)
                        (let ((tag-list (org-export-get-tags headline info)))
                          (and tag-list
                               (concat "     " (org-make-tag-string tag-list))))))
             (priority
              (and (plist-get info :with-priority)
                   (let ((char (org-element-property :priority headline)))
                     (and char (format "[#%c] " char)))))
             ;; Headline text without tags.
             (heading (concat todo priority title))
             (style (plist-get info :md-headline-style)))
        (cond
         ;; Cannot create a headline.  Fall-back to a list.
         ((or (org-export-low-level-p headline info)
              (not (memq style '(atx setext)))
              (and (eq style 'atx) (> level 6))
              (and (eq style 'setext) (> level 2)))
          (let ((bullet
                 (if (not (org-export-numbered-headline-p headline info)) "-"
                   (concat (number-to-string
                            (car (last (org-export-get-headline-number
                                        headline info))))
                           "."))))
            (concat bullet (make-string (- 4 (length bullet)) ?\s) heading tags "\n\n"
                    (and contents (replace-regexp-in-string "^" "    " contents)))))
         (t
          (let ((anchor
                 (and (org-md--headline-referred-p headline info)
                      (format "<a id=\"%s\"></a>"
                              (or (org-element-property :CUSTOM_ID headline)
                                  (org-export-get-reference headline info))))))
            (concat (org-md--headline-title style level heading anchor tags)
                    contents)))))))


  ;; https://emacs.stackexchange.com/questions/52057/export-only-visible-entries-of-sparse-tree
  (defun eqyiel/org-match-sparse-tree (match &optional match-body parents-body)
    "Create a custom sparse tree that only shows matched headings and parents.
    For MATCH see `org-match-sparse-tree'.
    If MATCH-BODY is non-nil the bodies of the matches are shown.
    If PARENTS-BODY is non-nil the bodies of the parents are shown.

  `org-highlight-sparse-tree-matches' is t by default, but required."
    ;; Create the sparse tree.
    (org-match-sparse-tree nil match)
    (let ((pt-first (save-excursion
                      (org-first-headline-recenter)
                      (move-beginning-of-line nil)
                      (point)))
          (hls org-occur-highlights))
      ;; Hide everything.
      (outline-flag-region pt-first (point-max) t)
      ;; For each occur highlight overlay (the matches).
      (dolist (hl hls)
        (save-excursion
          (goto-char (overlay-start hl))
          ;; Unhide match.
          (outline-show-heading)
          (when match-body (outline-show-entry))
          ;; Unhide parents.
          (while (org-up-heading-safe)
            (outline-show-entry)
            (when parents-body (outline-show-entry))))))
    ;; Hide all archived subtrees again.
    (org-hide-archived-subtrees (point-min) (point-max)))

  (defun eqyiel/org-export-before-processing-hook (backend)
  "BACKEND is one of `org-export-backends`."
    (if (eq backend 'rkm-md)
        (flush-lines (concat "\\[" (substring org-ts-regexp 1 -1) "\\]"))
      nil))
  ;; (org-export-to-buffer 'rkm-md "*Markdown output*" nil nil t nil nil nil)

  (defun eqyiel/create-markdown-report-for-yesterday ()
    (interactive)
    (eqyiel/org-match-sparse-tree
     "+CLOSED<=<-1d>+CLOSED>=<today>" t)
    (org-export-to-buffer 'rkm-md "*Markdown output*" nil nil t nil nil nil))

  (defun eqyiel/create-markdown-report-for-today ()
    (interactive)
    (eqyiel/org-match-sparse-tree
     (concat "TODO={DONE\\|CANCELLED\\|WAITING}"
             "+CLOSED>=\""
             (format-time-string "[%Y-%m-%d %a %H:%M]" (current-time))
             "\"") t)
     ;; (concat "TODO={DONE\\|CANCELLED\\|WAITING}"
     ;;         "+CLOSED>=\""
     ;;         (format-time-string "[%Y-%m-%d %a %H:%M]" (current-time))
     ;;         "\"") t)
     ;; (concat "+TODO=\"DONE\""
     ;;         "+CLOSED>=\""
     ;;         (format-time-string "[%Y-%m-%d]" (current-time))
     ;;         "\"") t)
    (org-export-to-buffer 'rkm-md "*Markdown output*" nil nil t nil nil nil))

  (use-package org
    :config
    (setq
     org-adapt-indentation t
     org-image-actual-width '(400)
     org-directory eqyiel/org-directory
     org-todo-keywords
     '((sequence "TODO(t@)" "WAITING(w@)" "|" "DONE(d@)" "DEFERRED(.@)" "CANCELLED(x@)" ))
     org-log-done 'time
     org-agenda-files
     (file-expand-wildcards
      (concat eqyiel/org-directory "/*.org*"))
     org-descriptive-links nil
     org-src-tab-acts-natively t
     org-src-preserve-indentation nil
     org-confirm-babel-evaluate nil
     org-export-babel-evaluate nil
     org-export-default-language "en"
     org-export-before-processing-hook '(eqyiel/org-export-before-processing-hook)
     org-capture-templates
     `(("t" "Todo" entry (file ,(eqyiel/maybe-create-org-agenda-file "inbox.org"))
        "* TODO %?
  SCHEDULED: %T
  :PROPERTIES:
  :CREATED: %U
  :END:
  "
        :empty-lines 1 :kill-buffer t))
     org-capture-before-finalize-hook 'eqyiel/org-capture-before-finalize-hook
     ;; realign tags after capture
     org-capture-after-finalize-hook 'org-align-all-tags
     org-highlight-sparse-tree-matches t
     org-agenda-custom-commands
     '(("n" "Agenda and all TODOs"
        ((agenda "")
         (alltodo "")))
       ("d" "Tasks done yesterday"
        ((tags
          (concat "+TODO=\"DONE\""
                  "+CLOSED>=\""
                  (format-time-string "[%Y-%m-%d]" (time-subtract (current-time) (days-to-time 1)))
                  "\""))))
       ("x" "Tasks done today"
        ((tags (concat "+TODO=\"DONE\"" "+CLOSED>=\"" (format-time-string "[%Y-%m-%d]" (current-time)) "\""))))))

    :init
    (progn
      (defvar eqyiel/org-src-lang-modes
        '(("Awk" . awk)
          ("C" . C)
          ("R" . R)
          ("Asymptote" . asymptote)
          ("Calc" . calc)
          ("Clojure" . clojure)
          ("CSS" . css)
          ("Ditaa" . ditaa)
          ("Dot" . dot)
          ("Emacs Lisp" . emacs-lisp)
          ("Forth" . forth)
          ("Fortran" . fortran)
          ("Gnuplot" . gnuplot)
          ("Haskell" . haskell)
          ("IO" . io)
          ("J" . J)
          ("Java" . java)
          ("Javascript" . js)
          ("LaTeX" . latex)
          ("Ledger" . ledger)
          ("Lilypond" . lilypond)
          ("Lisp" . lisp)
          ("Makefile" . makefile)
          ("Maxima" . maxima)
          ("Matlab" . matlab)
          ("Mscgen" . mscgen)
          ("Ocaml" . ocaml)
          ("Octave" . octave)
          ("Org" . org)
          ("Perl" . perl)
          ("Pico Lisp" . picolisp)
          ("PlantUML" . plantuml)
          ("Python" . python)
          ("Ruby" . ruby)
          ("Sass" . sass)
          ("Scala" . scala)
          ("Scheme" . scheme)
          ("Screen" . screen)
          ("Shell Script" . shell)
          ("Shen" . shen)
          ("Sql" . sql)
          ("Sqlite" . sqlite)
          ("Stan" . stan)
          ("ebnf2ps" . ebnf2ps))
        "This list is actually from `org-babel-load-languages', but it's not
  exposed as a variable.  It might change from time to time so be sure to look
  back there.")

      (defun eqyiel/org-clock-sum-today ()
        "Visit each file in `org-agenda-files' and return the total time of
    today's clocked tasks in minutes."
        (let ((files (org-agenda-files))
              (total 0))
          (org-agenda-prepare-buffers files)
          (dolist (file files)
            (with-current-buffer (find-buffer-visiting file)
              (setq total (+ total (org-clock-sum-today)))))
          total))

      (defun eqyiel/org-archive-done-tasks ()
        (interactive)
        (org-map-entries 'org-archive-subtree "/DONE" 'file))

      (defun eqyiel/org-select-src-lang-mode ()
        "Select a language mode from the alist of languages org-mode groks."
        (interactive)
        (let* ((selected-key
                (completing-read
                 "Select language: "
                 (seq-reduce
                  (lambda (prev next)
                    (if (not (member next prev))
                        (cons next prev)
                      prev))
                  (mapcar
                   (lambda (arg) (car arg))
                   eqyiel/org-src-lang-modes)
                  '())))
               (selected-value
                (cdr
                 (assoc selected-key eqyiel/org-src-lang-modes))))
          (insert
           (if selected-value (symbol-name selected-value)
             selected-key))))

      (org-babel-do-load-languages
       'org-babel-load-languages
       '((js . t)
         (emacs-lisp . t)
         (shell . t)))

      (require 'ox-gfm)
      (org-export-define-derived-backend 'rkm-md 'gfm
        :filters-alist '((:filter-parse-tree . org-md-separate-elements))
        :menu-entry
        '(?R "Export to Markdown"
             ((?R "To temporary buffer"
                  (lambda (a s v b) (org-gfm-export-as-markdown a s v)))
              (?g "To file" (lambda (a s v b) (org-gfm-export-to-markdown a s v)))
              (?o "To file and open"
                  (lambda (a s v b)
                    (if a (org-gfm-export-to-markdown t s v)
                      (org-open-file (org-gfm-export-to-markdown nil s v)))))))
        :translate-alist '((inner-template . org-gfm-inner-template)
                           (paragraph . org-gfm-paragraph)
                           (headline . eqyiel/org-md-headline)
                           (strike-through . org-gfm-strike-through)
                           (src-block . org-gfm-src-block)
                           (table-cell . org-gfm-table-cell)
                           (table-row . org-gfm-table-row)
                           (table . org-gfm-table))))
    :bind
    (("<f12>" . org-capture)
     ("C-c 0" . org-capture)
     ("<f7>" . org-agenda)
     ("C-c 7" . org-agenda)
     :map org-mode-map
     ("M-p" . eqyiel/org-prev-visible-heading)
     ("M-n" . org-next-visible-heading))
    :load-path "site-lisp/org-mode"
    :hook ((org-capture-prepare-finalize-hook . eqyiel/org-capture-prepare-finalize-hook)
           (org-mode-hook . eqyiel/org-mode-hook))
    :demand t)

#+END_SRC

** ~ox-gfm~                                                          :melpa:
Export ~org~ files to Github-flavoured markdown.

#+BEGIN_SRC emacs-lisp
  (use-package ox-gfm :demand t)
#+END_SRC

** ~pass~                                                            :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package pass :demand t)
#+END_SRC

** ~php-mode~                                                        :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package php-mode)
#+END_SRC

** ~prettier-emacs~                                                  :melpa:
#+BEGIN_SRC emacs-lisp
  (defun eqyiel/prettier-js-mode-hook ()
    "Maybe format buffer with prettier on save."
    (require 'prettier-js)
    (let ((js-project-roots (eqyiel/node-find-js-project-roots)))
      (when (eqyiel/node-should-use-package-p js-project-roots '(prettier))
        (progn
          (prettier-js-mode)
          ;; prefer the project's local prettier package before any global one
          (let ((local-project-prettier-executable
                 (eqyiel/node-find-project-local-executable-path
                  js-project-roots '(prettier))))
            (when local-project-prettier-executable
              (set (make-local-variable 'prettier-js-command)
                   local-project-prettier-executable)))))))

  (defun eqyiel/prettier-web-mode-hook ()
    "Enable Prettier if `web-mode-content-type' is something supported by Prettier."
    (when (or (string-equal web-mode-content-type "javascript")
              (string-equal web-mode-content-type "jsx"))
      (eqyiel/prettier-js-mode-hook)))

  (use-package prettier-js
    :hook (((js-mode-hook js2-mode-hook markdown-mode-hook typescript-mode-hook graphql-mode-hook) . eqyiel/prettier-js-mode-hook)
           (web-mode-hook . eqyiel/prettier-web-mode-hook))
    :config (setq prettier-js-command "prettier"))
#+END_SRC

** ~projectile~                                                      :melpa:

#+BEGIN_SRC emacs-lisp
  (use-package projectile
    :config
    (progn
      ;; monkeypatch `projectile-current-project-files' to use fd, it's much faster.
      (defun eqyiel/projectile-current-project-files ()
        "Return a list of files for the current project."
        (let ((files
               (and projectile-enable-caching
                    (gethash (projectile-project-root)
                             projectile-projects-cache))))
          ;; nothing is cached
          (unless files
            (when projectile-enable-caching
              (message "Empty cache. Projectile is initializing cache..."))
            (setq files
                  (split-string
                   (shell-command-to-string
                    (concat
                     "fd '' --hidden "
                     (directory-file-name (projectile-project-root))))))
            ;; cache the resulting list of files
            (when projectile-enable-caching
              (projectile-cache-project (projectile-project-root) files)))

          (projectile-sort-files files))))
    :init
    (progn
      ;; Default keybinding was changed from "C-c p" to "C-c C-p" in
      ;; https://github.com/bbatsov/projectile/commit/b90b950eead64b171d528098d186c19804739aa0
      (setq projectile-keymap-prefix (kbd "C-c p"))
      (use-package ivy)
      (projectile-global-mode)
      (advice-add
       'projectile-current-project-files
       :override
       'eqyiel/projectile-current-project-files)
      (setq projectile-completion-system 'ivy
            projectile-globally-ignored-directories
            (append '("dist" "node_modules")
                    projectile-globally-ignored-directories)))
    :bind ("<f5>" . projectile-compile-project))
#+END_SRC

** ~python-mode~                                                   :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package python-mode
    :bind
    (:map inferior-python-mode-map
          ("C-d" . eqyiel/comint-delchar-or-eof-or-kill-buffer)))
#+END_SRC

** ~rainbow-mode~                                                     :elpa:
#+BEGIN_SRC emacs-lisp
  (use-package rainbow-mode :commands rainbow-turn-on)
#+END_SRC

** ~recentf~                                                       :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package recentf
    :init
    (progn
      (require 'recentf)
      (setq
       recentf-save-file (concat eqyiel/xdg-cache-home "/emacs/.recentf")
       recentf-max-saved-items 1000)
      (recentf-mode))
    :demand t)
#+END_SRC

** ~rjsx-mode~                                                       :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package rjsx-mode
    :hook ((rjsx-mode-hook . eqyiel/js2-mode-hook)
           (rjsx-mode-hook . eqyiel/company-rjsx-mode-hook)
           (rjsx-mode-hook . eqyiel/flycheck-rjsx-mode-hook))
    :config
    (progn
      (use-package web-mode)
      (use-package flycheck)
      (use-package company)

      (defun eqyiel/toggle-rjsx-mode-to-web-mode ()
        (interactive)
        (web-mode))

      (defun eqyiel/company-rjsx-mode-hook ()
        (set (make-local-variable 'company-backends)
             '((company-files
                company-lsp
                company-keywords))))

      (defun eqyiel/flycheck-rjsx-mode-hook ()
        (require 'flycheck)
        (require 'flycheck-flow)
        (flycheck-mode)
        (let ((js-project-roots (eqyiel/node-find-js-project-roots)))
          (progn
            (when (eqyiel/node-should-use-package-p js-project-roots '(eslint))
              (let ((local-project-eslint-executable
                     (eqyiel/node-find-project-local-executable-path
                      js-project-roots '(eslint))))
                (when local-project-eslint-executable
                  (set (make-local-variable 'flycheck-javascript-eslint-executable)
                       local-project-eslint-executable))))
            (when (eqyiel/node-should-use-package-p js-project-roots '(flow flow-bin))
              (let ((local-project-flow-executable
                     (eqyiel/node-find-project-local-executable-path
                      js-project-roots '(flow flow-bin))))
                (when local-project-flow-executable
                  (set (make-local-variable 'flycheck-javascript-flow-executable)
                       local-project-flow-executable)))))))

      ;; https://emacs.stackexchange.com/questions/22044/treat-shebang-as-a-comment
      (modify-syntax-entry ?# ". 1" rjsx-mode-syntax-table)
      (modify-syntax-entry ?! ". 2b" rjsx-mode-syntax-table))
    :init (progn
            (require 'flycheck)
            (require 'flycheck-flow)
            (flycheck-add-mode 'javascript-flow 'rjsx-mode)
            (flycheck-add-mode 'javascript-flow-coverage 'rjsx-mode))
    :mode (("\\.js$" . rjsx-mode)
           ("\\.jsx$" . rjsx-mode))
    :bind (:map rjsx-mode-map
                ("C-M-s-\"" . eqyiel/toggle-rjsx-mode-to-web-mode)
                ("H-'" . eqyiel/toggle-rjsx-mode-to-web-mode)))
#+END_SRC

** ~savehist~                                                      :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package savehist
    :init
    (savehist-mode 1)
    :config
    (setq
     savehist-file
     (concat eqyiel/xdg-cache-home "/emacs/history")
     history-length 10000)
    :demand t)
#+END_SRC

** ~saveplace~                                                     :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package saveplace
    :init
    (progn
      (setq save-place-file
            (concat eqyiel/xdg-cache-home "/emacs/saveplace"))
      (setq-default save-place t))
    :demand t)
#+END_SRC

** ~scss-mode~                                                       :melpa:
#+BEGIN_SRC emacs-lisp
  (defun eqyiel/scss-mode-hook ()
    "Make Emacs comment features prefer double slash instead of block comment syntax "
    ;; use // instead of /* */
    (set (make-local-variable 'comment-start) "//")
    (set (make-local-variable 'comment-end) "")
    (set (make-local-variable 'comment-continue) "//"))

  (use-package scss-mode
    :hook (scss-mode-hook . eqyiel/scss-mode-hook))
#+END_SRC

** ~shell-mode~                                                    :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package shell
    :bind
    (:map shell-mode-map
          ("C-d" . eqyiel/comint-delchar-or-eof-or-kill-buffer)))
#+END_SRC

** ~shell-script-mode~                                             :builtin:
#+BEGIN_SRC emacs-lisp
  (defun eqyiel/setup-sh-mode ()
    (setq sh-basic-offset 2
          sh-indentation 2))

  (use-package shell-script-mode
    :hook (sh-mode-hook . eqyiel/setup-sh-mode))
#+END_SRC

** ~skewer-mode~                                                     :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package skewer-mode)
#+END_SRC

** ~smartparens~                                                     :melpa:
#+BEGIN_SRC emacs-lisp
  (defun eqyiel/get-derived-mode-parents (mode)
    (when (and mode (boundp 'derived-mode-parents))
      (cons mode (derived-mode-parents (get mode 'derived-mode-parent)))))

  (use-package smartparens
    :init
    (progn
      (advice-add
       'sp-splice-sexp-killing-around
       :before-until
       ;; Don't steal M-r in comint-mode or modes derived from comint-mode.
       (lambda (&rest args)
         (when (or (eq major-mode 'comint-mode)
                   (member 'comint-mode
                           (eqyiel/get-derived-mode-parents major-mode)))
           (comint-history-isearch-backward-regexp))))
      (smartparens-global-mode 1))
    :config
    (progn
      (sp-use-paredit-bindings)
      (setq sp-autoskip-closing-pair 'always
            sp-ignore-modes-list ;; Also be smart in the minibuffer.
            (delete 'minibuffer-inactive-mode sp-ignore-modes-list))
      (sp-local-pair 'org-mode "~" "~")
      (sp-local-pair 'org-mode "=" "=")
      (sp-local-pair 'org-mode "_" "_")
      (sp-local-pair 'org-mode "/" "/"))
    :demand t
    :diminish smartparens-mode)
#+END_SRC

** ~solarized-theme~                                                 :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package solarized-theme
    :init
    (progn
      (setq custom-safe-themes '("d677ef584c6dfc0697901a44b885cc18e206f05114c8a3b7fde674fce6180879" default)
            solarized-use-variable-pitch nil
            solarized-scale-org-headlines nil)
      (load-theme 'solarized-light t))
    :demand t)

#+END_SRC

** ~sql-indent~                                                       :elpa:
#+BEGIN_SRC emacs-lisp
(use-package sql-indent :hook (sql-mode-hook . sqlind-minor-mode))
#+END_SRC

** ~subword~                                                       :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package subword
    :init (global-subword-mode 1)
    :diminish subword-mode
    :demand t)
#+END_SRC

** ~swift-mode~                                                      :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package swift-mode
    :config
    (progn
      (use-package flycheck
        :init
        (progn
          (require 'warnings)
          (flycheck-define-checker swiftlint
            "Flycheck plugin for Swiftlint"
            :commands swiftlint
            :error-patterns
            ((error line-start (file-name) ":" line ":" column ": "
                    "error: " (message) line-end)
             (warning line-start (file-name) ":" line ":" column ": "
                      "warning: " (message) line-end))

            :modes swift-mode)))
      (setq swift-indent-offset 2))
    :init
    (progn '((add-to-list 'flycheck-checkers 'swift)
             (add-to-list 'flycheck-checkers 'swiftlint)
             (flycheck-add-next-checker 'swiftlint '(t . swift)))))
#+END_SRC

** ~swiper~ and friends                                              :melpa:

This is a new one for me.  See [[http://oremacs.com/swiper/#key-bindings-for-single-selection-action-then-exit-minibuffer][here]] for more info on key bindings.

#+BEGIN_SRC emacs-lisp
  (use-package smex
    :config
    (setq
     smex-save-file
     (concat eqyiel/xdg-cache-home "/emacs/smex-items"))
    :demand t)

  (use-package counsel
    :init (use-package ivy)
    :config
    ;; Override `counsel-rg-base-command' with the flag to follow symlinks, but do
    ;; it in a way that won't change the output if evaluated twice by getting the
    ;; special `standard-value' property installed by `defcustom' (as distinct from
    ;; `customized-value' and `saved-value')
    ;;
    ;; https://emacs.stackexchange.com/a/3024
    ;; https://stackoverflow.com/a/17396928
    (custom-set-variables
     `(counsel-rg-base-command
       ,(concat
         (s-replace-regexp
          "[\[:space:]]?%s[\[:space:]]?.?"
          " --follow %s ."
          (eval (car (get 'counsel-rg-base-command 'standard-value))))
         ;; Further workaround for how ripgrep returns a fatal error code despite
         ;; a partial result.
         ;; https://github.com/hlissner/doom-emacs/issues/3038#issuecomment-624165004
         " || true"
         )))
    :bind
    (("M-x" . counsel-M-x)
     ("C-x C-f" . counsel-find-file)
     ("<f1> f" . counsel-describe-function)
     ("<f1> v" . counsel-describe-variable)
     ("<f1> l" . counsel-find-library)
     ("<f2> i" . counsel-info-lookup-symbol)
     ("<f2> u" . counsel-unicode-char)
     ("C-c g" . counsel-git)
     ("C-c j" . counsel-git-grep)
     ("C-c K" . eqyiel/counsel-rg-from-current-directory)
     ("C-x l" . counsel-locate))
    :demand t)

  (defun eqyiel/counsel-projectile-rg-around (orig-fun &rest args)
    "If `projectile-counsel-rg' throws an error, fallback to regular `counsel-rg'."
    (when (not (condition-case nil (apply orig-fun args) (error nil)))
      (apply 'counsel-rg args)))

  (advice-add 'counsel-projectile-rg :around 'eqyiel/counsel-projectile-rg-around)

  (defun eqyiel/counsel-rg-from-current-directory ()
    (interactive)
    (apply 'counsel-rg `(nil ,default-directory)))

  (use-package avy
    :bind (("C-:" . avy-goto-char)
           ("C-'" . avy-goto-char-2)
           ("M-g f" . avy-goto-line)
           ("M-g w" . avy-goto-word-1)
           ("M-g e" . avy-goto-word-0)
           ("M-g o" . avy-org-refile-as-child)
           ("M-g j" . avy-org-goto-heading-timer))
    :demand t)

  (use-package ivy
    :config
    (setq ivy-use-virtual-buffers t)
    :init (ivy-mode 1)
    :bind
    (:map
     ivy-minibuffer-map
     ("C-s" . ivy-next-line-or-history)
     ("C-r" . ivy-previous-line-or-history)
     ("C-m" . ivy-alt-done)
     ("C-j" . ivy-immediate-done)
     :map
     swiper-map
     ("C-r" . ivy-previous-line-or-history)
     ("C-s" . ivy-next-line-or-history)
     ("H-'" . swiper-avy)
     :map
     counsel-find-file-map
     ("C-r" . ivy-previous-line)
     ("C-s" . ivy-next-line)
     :map
     counsel-git-grep-map
     ("C-r" . ivy-previous-line)
     ("C-s" . ivy-next-line)
     :map
     ivy-switch-buffer-map
     ("C-r" . ivy-previous-line)
     ("C-s" . ivy-next-line))
    :demand t
    :diminish ivy-mode)

  (use-package swiper
    :bind
    (("C-s" . swiper-isearch)
     ("C-r" . swiper-isearch))
    :init
    (progn
      (use-package ivy)
      (add-to-list 'swiper-font-lock-exclude 'nix-mode))
    :demand t)
#+END_SRC

** ~tide~                                                            :melpa:

#+BEGIN_SRC emacs-lisp
  (defun eqyiel/ts-check-tag-present-p ()
    "Return true if the '// @ts-check' tag is present in the current buffer."
    (save-excursion
      (goto-char (point-min))
      (let (stop found)
        (while (not stop)
          (when (not (re-search-forward "[^\n[:space:]]" nil t))
            (setq stop t))
          (if (equal (point) (point-min))
              (setq stop t)
            (backward-char))
          (cond ((or (looking-at "//+[ ]*@ts-check")
                     (looking-at "/\\**[ ]*@ts-check"))
                 (setq found t)
                 (setq stop t))
                ((looking-at "//")
                 (forward-line))
                ((looking-at "/\\*")
                 (when (not (re-search-forward "*/" nil t))
                   (setq stop t)))
                (t (setq stop t))))
        found)))

  (defun eqyiel/ts-tsserver-find-executable ()
    "Find tsserver executable in node_modules, if it exists."
    (let ((js-project-roots (eqyiel/node-find-js-project-roots)))
      (when (eqyiel/node-should-use-package-p
             js-project-roots '(typescript tsserver))
        (eqyiel/node-find-project-local-executable-path
         js-project-roots '(typescript tsserver)))))

  (defun eqyiel/ts-should-enable ()
    "Return t if this looks like a typescript project."
    (let ((tsserver-executable (eqyiel/ts-tsserver-find-executable)))
      (and (buffer-file-name) ;; can be nil, in ediff buffers for example
           (or (string-equal "ts" (file-name-extension (buffer-file-name)))
               (string-equal "tsx" (file-name-extension (buffer-file-name)))
               (eqyiel/ts-check-tag-present-p))
           (or (locate-dominating-file (buffer-file-name) "jsconfig.json")
               (locate-dominating-file (buffer-file-name) "tsconfig.json"))
           tsserver-executable)))

  (defun eqyiel/ts-tide-enable-maybe ()
    "Use `tide' if this is a typescript project and tsc is configured."
    (interactive)
    (when (eqyiel/ts-should-enable)
      (progn
        ;; Run nix-buffer if necessary to put things like node in PATH
        (eqyiel/nix-buffer-find-file-hook)
        (setq-local tide-tsserver-executable (eqyiel/ts-tsserver-find-executable))
        (tide-setup)
        (tide-hl-identifier-mode))))

  (defun eqyiel/ts-maybe-format-before-save ()
    "Use tsfmt if this is a typescript project and prettier is not configured."
    (interactive)
    (let* ((js-project-roots (eqyiel/node-find-js-project-roots))
          (should-use-ts (eqyiel/ts-should-enable))
          (should-use-prettier
           (eqyiel/node-should-use-package-p js-project-roots '(prettier))))
      (when (and should-use-ts (not should-use-prettier))
        (tide-format-before-save))))

  (use-package tide
    :after (typescript-mode company flycheck)
    :mode (("\\.ts$" . typescript-mode) ("\\.tsx$" . typescript-mode))
    :config (flycheck-add-next-checker 'javascript-eslint 'jsx-tide 'append)
    :hook (((js2-mode-hook web-mode-hook rjsx-mode-hook typescript-mode-hook) . eqyiel/ts-tide-enable-maybe)
           (before-save . eqyiel/ts-maybe-format-before-save)))
#+END_SRC

** ~typescript-mode~                                                 :melpa:

#+BEGIN_SRC emacs-lisp
  (defun eqyiel/typescript-mode-remove-annoying-bindings ()
    "Remove bindings that break `smartparens' and muscle memory."
    (dolist (key '("{" "}" "(" ")" ":" ";" ","))
      (local-unset-key key))
    (dolist (key '("\"" "\'"))
      (local-unset-key key))
    (local-unset-key (kbd "C-c '")))

  (use-package typescript-mode
    :hook (typescript-mode-hook . eqyiel/typescript-mode-remove-annoying-bindings))
#+END_SRC

** ~tiny~                                                            :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package tiny
    :bind (("H-;" . tiny-expand)))
#+END_SRC

** ~tramp~                                                         :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package tramp
    :config
    (setq
     ;; Useful for debugging `tramp'
     ;; tramp-debug-buffer t
     ;; tramp-verbose 10
     tramp-persistency-file-name "~/.cache/emacs/tramp"
     tramp-auto-save-directory "~/.cache/emacs/backup"
     ;; When `tramp' successfully logs in but hangs, it's probably because
     ;; `tramp-terminal-prompt-regexp' doesn't recognise it:
     ;; http://stackoverflow.com/a/8363532
     tramp-shell-prompt-pattern "\\(?:^\\|\r\\)[^]#$%>\n]*#?[]#$%>].* *\\(^[\\[[0-9;]*[a-zA-Z] *\\)*")
    :config
    (progn
      ;; Uses sudo password for the user defined in ~/.ssh/config, not root password.
      ;; No need to allow ssh for root.
      (add-to-list 'tramp-default-proxies-alist '(".*" "\\`root\\'" "/ssh:%h:"))
      (add-to-list 'tramp-default-proxies-alist '("\\`localhost\\'" "\\`root\\'" nil))))
#+END_SRC

** ~unfill~                                                          :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package unfill :demand t :bind ("M-q" . unfill-toggle))
#+END_SRC

** ~uniquify~                                                      :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package uniquify
    :config
    (setq uniquify-buffer-name-style 'forward)
    :demand t)
#+END_SRC

** ~visual-fill-column~                                              :melpa:
This is a pretty good replacement  for ~longlines-mode~.

#+BEGIN_SRC emacs-lisp
  (defun eqyiel/longlines-mode ()
    "Replacement for the deprecated `longlines-mode'."
    (interactive)
    (let ((state (if (and visual-fill-column-mode visual-line-mode) -1 1)))
      (progn (visual-fill-column-mode state)
             (visual-line-mode state))))

  (defalias 'longlines-mode 'eqyiel/longlines-mode)

  (use-package visual-fill-column
    :commands (visual-fill-column-mode)
    :config (require 'simple)
    :demand t)
#+END_SRC

** ~web-mode~                                                        :melpa:
#+BEGIN_SRC emacs-lisp
  (defun eqyiel/toggle-web-mode-to-js2-mode ()
    (interactive)
    (require 'js2-mode)
    (if (string-equal web-mode-content-type "jsx")
        (js2-jsx-mode)
      (js2-mode)))

  (defun eqyiel/toggle-web-mode-to-rjsx-mode ()
    (interactive)
    (rjsx-mode))

  (defun eqyiel/flycheck-web-mode-hook ()
    (when (or (string-equal web-mode-content-type "javascript")
              (string-equal web-mode-content-type "jsx"))
      (progn
        (require 'flycheck)
        (require 'flycheck-flow)
        (flycheck-mode)
        (let ((js-project-roots (eqyiel/node-find-js-project-roots)))
          (progn
            (when (eqyiel/node-should-use-package-p js-project-roots '(eslint))
              (let ((local-project-eslint-executable
                     (eqyiel/node-find-project-local-executable-path
                      js-project-roots '(eslint))))
                (when local-project-eslint-executable
                  (set (make-local-variable 'flycheck-javascript-eslint-executable)
                       local-project-eslint-executable))))
            (when (eqyiel/node-should-use-package-p js-project-roots '(flow))
              (let ((local-project-flow-executable
                     (eqyiel/node-find-project-local-executable-path
                      js-project-roots '(flow))))
                (when local-project-flow-executable
                  (set (make-local-variable 'flycheck-javascript-flow-executable)
                       local-project-flow-executable)))))))))

  (use-package web-mode
    :init
    (progn
      (require 'flycheck)
      (require 'flycheck-flow)
      (flycheck-add-mode 'javascript-eslint 'web-mode)
      (flycheck-add-mode 'javascript-flow 'web-mode)
      (flycheck-add-mode 'javascript-flow-coverage 'web-mode))
    :mode ("\\.html?\\'" . web-mode)
    :hook (web-mode-hook . eqyiel/flycheck-web-mode-hook)
    :config
    (setq
     web-mode-enable-auto-indentation nil
     web-mode-markup-indent-offset 2
     web-mode-code-indent-offset 2
     web-mode-css-indent-offset 2
     web-mode-attr-indent-offset 2
     web-mode-sql-indent-offset 2
     web-mode-content-types-alist
     '(("jsx" . "\\.js[x]?\\'")))

    ;; https://emacs.stackexchange.com/questions/22044/treat-shebang-as-a-comment
    (modify-syntax-entry ?# ". 1" web-mode-syntax-table)
    (modify-syntax-entry ?! ". 2b" web-mode-syntax-table)
    :bind
    (:map web-mode-map
          ("C-M-s-\"" . eqyiel/toggle-web-mode-to-rjsx-mode)
          ("H-'" . eqyiel/toggle-web-mode-to-rjsx-mode)
          ("M-j" . newline-and-indent)
          ("C-c C-l" . flycheck-list-errors)))
#+END_SRC

** ~winner~                                                        :builtin:
#+BEGIN_SRC emacs-lisp
  (use-package winner
    :config (winner-mode t)
    :demand t)
#+END_SRC

** ~ws-butler~                                                       :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package ws-butler
    :init (ws-butler-global-mode)
    :diminish ws-butler-mode
    :demand t)
#+END_SRC

** ~yaml-mode~                                                       :melpa:
This package adds itself to ~auto-mode-alist~.

#+BEGIN_SRC emacs-lisp
  (use-package yaml-mode)
#+END_SRC

** ~yasnippet~                                                       :melpa:
#+BEGIN_SRC emacs-lisp
  (use-package yasnippet
    :init
    (progn
      (require 'warnings)
      (setq
       yas-snippet-dirs
       `(,(concat (directory-file-name user-emacs-directory) "/snippets"))
       yas-prompt-functions '(yas-ido-prompt))
      ;; Warning (yasnippet): ‘Snippet’ modified buffer in a backquote expression.
      ;; To hide this warning, add (yasnippet backquote-change) to
      ;; ‘warning-suppress-types’.
      (add-to-list 'warning-suppress-types 'yasnippet)
      (add-to-list 'warning-suppress-types 'backquote-change)
      (yas-global-mode t))
    :bind (("C-c TAB" . yas-expand))
    :diminish yas-minor-mode
    :demand t)
#+END_SRC
